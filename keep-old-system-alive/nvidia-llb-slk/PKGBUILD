# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
pkgname='nvidia-llb-slk'
pkgver=295.75
pkgrel=12
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')

makedepends=('linux-slk>=3.2.44' 'linux-slk<3.3' 'linux-slk-headers>=3.2' 'linux-slk-headers<3.3' "nvidia-llb-utils=${pkgver}")

_extramodules=3.2-SLK-external

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('61158fcfe5e4685e69f86de1f411a6dd1ab0e33082f377ccd2bc7469fde143f9')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('f47dfcb48dd3d8646f9f1a33eff4f455571d3260ad38be625f04066c11217544')
fi

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only

	_kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	cd $_pkg/kernel
	sed -e '/CFLAGS="$CFLAGS/s:-I$SOURCES/arch/x86/include:& -I$OUTPUT/arch/x86/include/generated:' -i conftest.sh
	sed -e 's:-Wsign-compare::g' -i Makefile.kbuild
 	make SYSSRC=/usr/lib/modules/$_kernver/build module
}

package_nvidia-llb-slk() {
	pkgdesc="NVIDIA drivers for linux-slk Long Lived Branch"
	depends=('linux-slk>=3.2' 'linux-slk<3.3' "nvidia-llb-utils=${pkgver}")
	conflicts=('nvidia-96xx' 'nvidia-173xx')
	provides=('nvidia')
	install=nvidia.install

	install -Dm644 "$srcdir/$_pkg/kernel/nvidia.ko" \
		"$pkgdir/usr/lib/modules/$_extramodules/$_pkgname/nvidia.ko"

	install -dm755 "$pkgdir/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"
	echo "blacklist nvidiafb" >> "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"

	# gzip all modules
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/nvidia.install"
}

# vim:set ft=sh:
