# Contributer: Tom Billiet (mouse256@ulyssis.org)
# Contributer: Ike Devolder (ike.devolder@herecura.be)

pkgname=jalbum
pkgver=8.9.3
pkgrel=1
pkgdesc="free web photo album software and photo gallery software"
url="http://jalbum.net/"
license=('custom' 'lgpl')
depends=('java-runtime')
arch=('any')
source=("http://jalbum.net/download/Jalbum$pkgver.zip"
	"jalbum.sh"
	"jalbum.desktop")
sha256sums=('e0ea311bde309b29b23ef6c63a8fb38fab421ee31ad07a7445c6f3cb22171c31'
            'd008cb21b97620a91a854902166806afefcc9799aa6fa8ad7ca5855b199b567c'
            '89c0eee9fe4ba8bea89f1c4c0a7943cc5dff8f4427cce67b9eda6f1a64dd930c')

build() {
	#remove batfiles and java sourcefiles
	cd Jalbum
	find \( -name "*.bat" -o -name "*.java" \) -delete

	#remove lib/sunos/windows
	rm -rf lib/{sunos,windows}

	#remove startupscript, we install one in usr/bin
	rm -f startjalbum.sh

	#make sure no executable files are here
	chmod -R u=rwX,go=rX *
}

package() {
	install -dm644 ${pkgdir}/usr/share/java/
	cp -a Jalbum ${pkgdir}/usr/share/java/Jalbum
	install -dm644 ${pkgdir}/usr/share/pixmaps
	ln -s "/usr/share/java/Jalbum/icons/JalbumApp48.png" "${pkgdir}/usr/share/pixmaps/jalbum.png"
	install -Dm644 $srcdir/jalbum.desktop $pkgdir/usr/share/applications/$pkgname.desktop
	install -Dm755 $srcdir/jalbum.sh $pkgdir/usr/bin/jalbum
}
