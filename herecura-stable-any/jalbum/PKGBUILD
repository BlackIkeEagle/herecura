# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributer: Tom Billiet (mouse256@ulyssis.org)

pkgname=jalbum
pkgver=9
pkgrel=1
pkgdesc="free web photo album software and photo gallery software"
url="http://jalbum.net/"
license=('custom' 'lgpl')
depends=('java-runtime')
arch=('any')
source=(
	"http://jalbum.net/download/Jalbum$pkgver.zip"
	"jalbum.sh"
	"jalbum.desktop"
)
sha256sums=('ed1693e72c22305834e77696884e449ee4c8a22934b1b409cdcc2017d81260b9'
            'e1efa97077ac34ba3473cc1440f3a1427baea61e3a807d80efa6cf95c33eaf32'
            '7a059d23bd78e18681e5d960b213ef0cd2d4f37954bee40340409b459436fb33')

build() {
	#remove batfiles and java sourcefiles
	cd Jalbum
	find \( -name "*.bat" -o -name "*.cmd" -o -name "*.java" -o -name "compile.sh" \) -delete

	#remove lib/sunos/windows
	rm -rf lib/{sunos,windows}

	#remove startupscript, we install one in usr/bin
	rm -f startjalbum.sh

	# remove distritbution text, it doesnt add usefull info
	rm -f distribution.txt

	#make sure no executable files are here
	chmod -R u=rwX,go=rX *
}

package() {
	install -dm755 ${pkgdir}/usr/share/
	cp -a Jalbum ${pkgdir}/usr/share/jalbum

	#move licence to the correct place
	install -dm755 ${pkgdir}/usr/share/licenses/
	cp -a ${pkgdir}/usr/share/${pkgname}/license ${pkgdir}/usr/share/licenses/${pkgname}
	rm -rf ${pkgdir}/usr/share/${pkgname}/license

	install -dm755 ${pkgdir}/usr/share/pixmaps
	ln -s "/usr/share/jalbum/icons/JalbumApp48.png" "${pkgdir}/usr/share/pixmaps/jalbum.png"
	install -Dm644 $srcdir/jalbum.desktop $pkgdir/usr/share/applications/$pkgname.desktop
	install -Dm755 $srcdir/jalbum.sh $pkgdir/usr/bin/jalbum
}
