pkgname=hamsimanager
_realname=HamsiManager
pkgver=1.0
pkgrel=1
pkgdesc="Hamsi Manager is a file manager,renamer,id3 tagger,information changer ,.... tool."
arch=('any')
url="http://hamsimanager.sourceforge.net"
license=('GPL3')
depends=('python2-qt')
optdepends=(
	'mplayer: optional player'
	'phonon: optional player'
)
source=("https://downloads.sourceforge.net/project/hamsimanager/$_realname-$pkgver.tar.gz"
	'hamsi.launcher'
	'compile.py')

_destdir="/usr/lib"

build() {
	cd ${_realname}
	sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" HamsiManager.py
	${srcdir}/compile.py $(find . -mindepth 1 -maxdepth 1 -type d | sed 's@./@@')
	find . -mindepth 2 -name "*.py" -delete
	mv ConfigureUpdate.py Update.py
	rm install.py
	sed "s@~InstallationDirectory~@${_destdir}/${pkgname}@" -i HamsiManager.desktop
	chmod -R u=rwX,go=rX *
}

package() {
	cd ${_realname}
	install -Dm644 ${srcdir}/${_realname}/HamsiManager.desktop \
		${pkgdir}/usr/share/applications/${pkgname}.desktop
	rm ${srcdir}/${_realname}/HamsiManager.desktop

	for lic in BeniOku COPYING ForDevelopers Kopya README; do
		install -Dm644 ${srcdir}/${_realname}/${lic} \
			${pkgdir}/usr/share/licences/${pkgname}/${lic}
		rm ${lic}
	done

	install -Dm755 ${srcdir}/hamsi.launcher \
		$pkgdir/usr/bin/${pkgname}
	sed "s@%DESTDIR%@${_destdir}/${pkgname}@" -i ${pkgdir}/usr/bin/${pkgname}

	cd ${srcdir}
	install -dm755 ${pkgdir}${_destdir}
	cp -a ${_realname} ${pkgdir}${_destdir}/${pkgname}
}
sha256sums=('33b983a11e89f1767f7f84bf524eeb92333684867b7e8e87bdc184fa67e677ef'
            '579745bfbe0f5ec06a34f8b87702f23d95a8ba06c907e35248a215a569511079'
            '595bcb1e158c3ab4cf12e0ee1ad8a0627f5980ab7be4eeb33fc1f7d93aa073e6')
