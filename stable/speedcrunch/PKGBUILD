# Maintainer: Antonio Rojas <nqn1976@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=speedcrunch
pkgver=0.11
pkgrel=1
pkgdesc="Simple but powerful calculator using Qt"
arch=('i686' 'x86_64')
url="http://speedcrunch.org/"
license=('GPL2')
depends=('qt4')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/speedcrunch/SpeedCrunch/archive/${pkgver}.tar.gz")

build() {
	cd SpeedCrunch-$pkgver/src
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4
	make
}

package() {
	cd SpeedCrunch-$pkgver/src
	make DESTDIR="$pkgdir" install
}

sha256sums=('3632f2b40ac628d2665c838643cc99c50e89b60f54ac2a82b4533d254f0cb6eb')
