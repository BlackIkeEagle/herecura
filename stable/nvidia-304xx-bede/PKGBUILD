# $Id$
# Maintainer : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-304xx
pkgver=304.64
_extramodules=3.7-BEDE-external
pkgrel=2
pkgdesc="NVIDIA drivers for linux, 304xx legacy branch"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-bede>=3.7' 'linux-bede<3.8' 'linux-bede-headers>=3.7' 'linux-bede-headers<3.8' "nvidia-304xx-utils=$pkgver")
conflicts=('nvidia' 'nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
            'uapi-3.7.patch')
    md5sums=('6964415cf648a5f4f38117b168369de2'
             '42f2ad1cf9b48b1da8ed82200a045a8a')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
            'uapi-3.7.patch')
    md5sums=('76007ed4b72afd334e26377b646f5f7e'
             '42f2ad1cf9b48b1da8ed82200a045a8a')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch -Np0 -i "${srcdir}/uapi-3.7.patch"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	depends=('linux-bede>=3.7' 'linux-bede<3.8' "nvidia-304xx-utils=${pkgver}")

    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
