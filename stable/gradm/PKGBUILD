# Contributors:
#   Jonathan Liu <net147@gmail.com>
#   henning mueller <henning@orgizm.net>

pkgname=gradm
pkgver=2.9.1
_timestamp=201309161709
pkgrel=1
pkgdesc='Administrative interface for the grsecurity Role Based Access Control system'
arch=('i686' 'x86_64')
url='http://grsecurity.net/'
license=('GPL2')
depends=('pam')
source=(
	"http://grsecurity.net/stable/$pkgname-$pkgver-$_timestamp.tar.gz"
	"http://grsecurity.net/stable/$pkgname-$pkgver-$_timestamp.tar.gz.sig"
)
sha256sums=(
	'2375c11405bd6df37b9f4334dca83d7c28bb407053b4b48e96837d3e80ab724c'
	'SKIP'
)

build() {
  cd "${pkgname}2"
  sed -i -e 's/^CFLAGS :=/CFLAGS +=/' -e 's:sbin:usr/bin:' Makefile
  make
}

package() {
  cd "${pkgname}2"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/dev"
}

