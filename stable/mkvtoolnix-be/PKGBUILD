# Maintainer: BlackEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=mkvtoolnix-be
pkgver=5.0.1
pkgrel=2
pkgdesc="Set of tools to create, edit and inspect Matroska files, with gui's"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'flac' 'libvorbis' 'file' 'boost-libs' 'lzo2')
makedepends=('boost' 'ruby' 'qt' 'wxgtk')
optdepends=(
	'wxgtk: gtk gui for mkvinfo and mmg'
	'qt: qt gui for mkvinfo and mmg'
)
conflicts=('mkvtoolnix' 'mkvmerge-gui')
install=mkvtoolnix.install
source=("http://www.bunkus.org/videotools/${pkgname/-be/}/sources/${pkgname/-be/}-${pkgver}.tar.bz2")

build() {
  cd "${srcdir}/${pkgname/-be/}-${pkgver}"

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  #return 1

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib \
			  --enable-gui \
			  --enable-qt
  ./drake
}

package () {
  cd "${srcdir}/${pkgname/-be/}-${pkgver}"

  ./drake DESTDIR="${pkgdir}" install
}
sha256sums=('eecc48d17c0583b6b071e710bfe88ae09439bd99bb90c694a2fed0651bb08f19')
