# $Id$
# Maintainer: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sébastien Luttringer <seblu@aur.archlinux.org>

pkgbase=virtualbox-modules-besrv
pkgname=('virtualbox-modules-besrv-host' 'virtualbox-modules-besrv-guest')
pkgver=4.2.6
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-besrv>=3.4' 'linux-besrv<3.5' 'linux-besrv-headers>=3.4' 'linux-besrv-headers<3.5'
    "virtualbox-host-dkms>=$pkgver"
    "virtualbox-guest-dkms>=$pkgver")
source=('modules-load-virtualbox-besrv'
    '60-vboxguest.rules')
md5sums=('f2200ed91b6ec089d16cc3ada5418c73'
    'ed1341881437455d9735875ddf455fbe')

_extramodules=3.4-BESRV-external

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    # dkms need modification to be run as user
    cp -r /var/lib/dkms .
    echo "dkms_tree='$srcdir/dkms'" > dkms.conf
    # build host modules
    msg2 'Host modules'
    dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
    # build guest modules
    msg2 'Guest modules'
    dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-modules-besrv-host() {
    pkgdesc="Kernel host modules for VirtualBox (linux-besrv)"
    license=('GPL')
    install=virtualbox-modules-besrv-host.install
    depends=('linux-besrv>=3.4' 'linux-besrv<3.5')
    provides=("virtualbox-host-modules=$pkgver")

    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
    install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

    # install config file in modules-load.d for out of the box experience
    install -Dm644 "$srcdir/modules-load-virtualbox-besrv" \
        "$pkgdir/usr/lib/modules-load.d/virtualbox-modules-besrv-host.conf"

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/virtualbox-modules-besrv-host.install"
}

package_virtualbox-modules-besrv-guest() {
    pkgdesc="Kernel guest modules for VirtualBox (linux-besrv)"
    license=('GPL')
    install=virtualbox-modules-besrv-guest.install
    depends=('linux-besrv>=3.4' 'linux-besrv<3.5')
    provides=("virtualbox-guest-modules=${pkgver}")

    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
    install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

    install -D -m 0644 "$srcdir/60-vboxguest.rules" \
        "$pkgdir/usr/lib/udev/rules.d/60-vboxguest-besrv.rules"

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/virtualbox-modules-besrv-guest.install"
}

# vim:set ft=sh et:
