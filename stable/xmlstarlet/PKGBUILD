# Maintainer: PyroPeter <abi1789@googlemail.com>
# Contributor: BlackEagle <ike.devolder@scarlet.be>
# Contributor: chochem <chochem@gmail.com>

pkgname=xmlstarlet
pkgver=1.3.1
pkgrel=1
pkgdesc="A set of tools to transform, query, validate, and edit XML documents"
arch=('i686' 'x86_64')
url="http://xmlstar.sourceforge.net/"
license=('MIT')
#depends=('zlib' 'libgcrypt')
depends=('libxslt')
#makedepends=('glibc' 'libxslt>=1.1.9' 'libxml2')
source=("xmlstarlet-$pkgver.tar.gz::http://sourceforge.net/projects/xmlstar/files/xmlstarlet/$pkgver/xmlstarlet-$pkgver.tar.gz/download")

build() {
  cd "$srcdir/$pkgname-$pkgver"
  local xsltlibs=$(pkg-config --libs libxslt libexslt)
  local xmllibs=$(pkg-config --libs libxml-2.0)

  LIBXSLT_PREFIX=/usr LIBXML_PREFIX=/usr LIBXSLT_LIBS="${xsltlibs}" LIBXML_LIBS="${xmllibs}" ./configure --prefix=/usr
  
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  cp Copyright $pkgdir/usr/share/licenses/$pkgname/COPYING
  ln -s /usr/bin/xml ${pkgdir}/usr/bin/xmlstarlet
}
sha256sums=('effe9aba3f8003d1da2b07aa54af4f3b5956bd09f061c499442415278425885b')
