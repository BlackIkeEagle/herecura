# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgname=eclipse-php
pkgver=3.0.2
_pkgtimestamp=v20120511142
pkgrel=3
pkgdesc="Eclipse IDE for PHP Developers"
arch=('i686' 'x86_64')
license=('EPL')
url='http://zend-sdk.googlecode.com/'
depends=('java-runtime' 'libxrender' 'fontconfig')
install=$pkgname.install
sha256sums=('1935f58979403057c7fc701ba9f876b60dd89cc1c3eac5eca61e792da2823731')
if [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	sha256sums=(${sha256sums[@]} 'ca8a424ea723335431787be3df6768953c5062fd2c25a3119f771a2eacbdc142')
else
	_arch='x86'
	sha256sums=(${sha256sums[@]} '34f81c5b6b9fe28e3aeb1f52c4dd4e7f140b76fc9220b7387b8514efd0249ff4')
fi
source=(
	"$pkgname.desktop"
	"http://zend-sdk.googlecode.com/files/$pkgname-$pkgver.$_pkgtimestamp-$_arch.tar.gz"
)

package() {
	install -dm755 "$pkgdir/opt/"
	mv "$srcdir/$pkgname" "$pkgdir/opt/"

	install -dm755 "$pkgdir/usr/bin"
	ln -sf "/opt/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"

	install -dm755 "$pkgdir/usr/share/pixmaps"
	ln -s "/opt/$pkgname/plugins/org.eclipse.platform_3.7.2.v201202080800/eclipse48.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
	install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
