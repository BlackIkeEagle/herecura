# Maintainer: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Contributor: aidanlinz
# Contributor: Spasswolf
# Contributor: DestroyFX
# Contributor: Rip-Rip
# Contributor: OvsInc

_pkgname=catalyst
_extramodules=3.4-BEDE-external

pkgname=$_pkgname-bede
pkgver=12.4
pkgrel=2
pkgdesc="AMD/ATI Catalyst drivers for linux-bede. fglrx kernel module only"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('linux-bede>=3.4' 'linux-bede<3.5' "catalyst-utils=$pkgver")
makedepends=("catalyst-utils=$pkgver" 'linux-bede>=3.4' 'linux-bede<3.5' 'linux-bede-headers>=3.4' 'linux-bede-headers<3.5')
conflicts=('catalyst-test' 'nvidia' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri')
provides=('catalyst')
install="$pkgname.install"

source=(
	"http://www2.ati.com/drivers/linux/amd-driver-installer-${pkgver/./-}-x86.x86_64.run"
	'makefile_compat.patch'
	'3.2.8.patch'
	'34.patch'
)

md5sums=(
	'e57d65cb9c9fa470d6d80072d5ab2785'
	'3e1b82bd69774ea808da69c983d6a43b'
	'32c8142c73dcfed5db426f494feda9e3'
	'a09e2916da3ff2b4ea9b0ad8cb52babe'
)

build() {
	_kernver="$(cat /lib/modules/$_extramodules/version)"

	## Unpack archive
	msg "Unpacking archive ..."
	/bin/sh ./amd-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files

	if [ "$CARCH" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "$CARCH" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	cd "$srcdir/archive_files/"

	patch -Np1 -i "$srcdir/makefile_compat.patch"
	#patch -Np1 -i "$srcdir/3.2.8.patch"
	(
		cd common/lib/modules/fglrx/build_mod/
		patch -Np1 -i "$srcdir/34.patch"
	)

	###--- cut ---### ati's code from their make.sh file

	# ==============================================================
	# resolve if we are running a SMP enabled kernel
	def_smp=-D__SMP__

	# ==============================================================
	# resolve whether we need to set PAGE_ATTR_FIX
	PAGE_ATTR_FIX=0

	# ==============================================================
	# resolve if we are running a MODVERSIONS enabled kernel

	def_modversions=''

	# ==============================================================
	# resolve if we are building for a kernel with a fix for CVE-2010-3081
	# On kernels with the fix, use arch_compat_alloc_user_space instead
	# of compat_alloc_user_space since the latter is GPL-only

	COMPAT_ALLOC_USER_SPACE=arch_compat_alloc_user_space

	###--- cut ---###


	cd "$srcdir/archive_files/common/lib/modules/fglrx/build_mod"
	cp "$srcdir/archive_files/arch/$_archdir/lib/modules/fglrx/build_mod/libfglrx_ip.a" ./
	cp 2.6.x/Makefile ./


	## Compiling module
	make -C "/lib/modules/$_kernver/build" SUBDIRS="`pwd`" ARCH=$BUILDARCH \
		MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
		PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}

package() {
	cd "$srcdir/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "$pkgdir/lib/modules/$_extramodules/$_pkgname/"
	install -m644 fglrx.ko "$pkgdir/lib/modules/$_extramodules/$_pkgname/"

	## License
	install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
	install -m644 "$srcdir/archive_files/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/"

	# gzip all modules
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir"/*.install
}
