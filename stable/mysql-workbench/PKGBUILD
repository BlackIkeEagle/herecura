# Maintainer: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mysql-workbench
pkgver=5.2.42
pkgrel=1
pkgdesc="A cross-platform, visual database design tool developed by MySQL"
arch=('i686' 'x86_64')
url="https://www.mysql.com/products/workbench/"
license=('GPL2')
depends=('libzip' 'libmysqlclient' 'lua' 'gtkmm' 'ctemplate'
	'libgnome-keyring' 'libgl' 'python-paramiko' 'python-pexpect'
	'pcre' 'tinyxml' 'libxml2' 'mysql-python' 'python-pysqlite' 'cairo'
	'python-cairo' 'hicolor-icon-theme' 'desktop-file-utils' 'libiodbc')

makedepends=('boost' 'curl' 'mesa')
options=('!libtool !distcc !ccache')

source=(
	"http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQLGUITools/${pkgname}-gpl-${pkgver}-src.tar.gz"
	'mysql-workbench-5.2.41-glib.patch'
)

md5sums=(
	'519d2e4bc878d79023379e1fef49b68c'
	'91d1f1fb614de54c4c455026c73f8afb'
)

build() {
	cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

	sed -i -e 's/python -c/python2 -c/gi' configure.in

	patch -Np1 -i $srcdir/'mysql-workbench-5.2.41-glib.patch'

	./autogen.sh
	./configure --prefix=/usr

	make
}

package() {
	cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

	make DESTDIR="${pkgdir}" install

	cd ${pkgdir}/usr/share/mysql-workbench/
	for i in *.py *.glade
	do
		ln -s /usr/share/mysql-workbench/$i ${pkgdir}/usr/lib/mysql-workbench/modules/
	done
}
