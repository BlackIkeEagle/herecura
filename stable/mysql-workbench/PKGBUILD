# vim:set ft=sh:
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname='mysql-workbench'
pkgver=6.1.7
pkgrel=3
pkgdesc="A cross-platform, visual database design tool developed by MySQL"
arch=('i686' 'x86_64')
url="https://www.mysql.com/products/workbench/"
license=('GPL2')
depends=('ctemplate' 'desktop-file-utils' 'gtkmm' 'hicolor-icon-theme'
	'libgnome-keyring' 'libiodbc' 'libzip' 'lua51' 'mysql-connector-c++'
	'python2' 'python2-paramiko' 'python2-pexpect' 'python2-pysqlite'
	'tinyxml' 'unixodbc' 'vsqlite++' 'swig')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager')
makedepends=('boost' 'curl' 'mesa' 'cmake')
install='mysql-workbench.install'

source=(
	"http://cdn.mysql.com/Downloads/MySQLGUITools/${pkgname}-community-${pkgver}-src.tar.gz"
	'arch_linux_profile.xml'
	'mysql-workbench-ctemplate.patch'
	'mysql-workbench-mariadb-10.patch'
	'mysql-workbench-no-check-for-updates.patch'
)

prepare() {
	cd "$pkgname-community-$pkgver-src"

	# Disable 'Help' -> 'Check for Updates'
	patch -Np1 < "${srcdir}"/mysql-workbench-no-check-for-updates.patch

	# fix deprecated calls to tpl->ReloadIfChanged
	# http://bugs.mysql.com/72585
	patch -Np1 < "${srcdir}"/mysql-workbench-ctemplate.patch

	# fix compilation with Mariadb
	patch -Np1 < "${srcdir}"/mysql-workbench-mariadb-10.patch
}

build() {
	cd "$pkgname-community-$pkgver-src"

	cmake . \
		-DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
		-DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
		-DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
		-DLUA_LIBRARY=/usr/lib/liblua5.1.so \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$pkgname-community-$pkgver-src"

	make DESTDIR="${pkgdir}" install
	install -D -m 0644 ${srcdir}/arch_linux_profile.xml ${pkgdir}/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB_5.5_Package\).xml

	# dirty trick to clean build env
	cd .. && rm -rf "$pkgname-community-$pkgver-src"
}

sha256sums=('06ac657f7f86f012f75fcf8fa59fc1d422134135485b887a4abd6c9ac2408898'
            '1aa3cc7bf83470e81eca766114a597fa57542316818732011c8ea67e52197c7e'
            'bbf6f3e56847cc012634e262295f34eaaa43ef32d04e1ec291315dabd6613042'
            '6fc7d88a4e43ba3ccf0fac8075e4e5ca253626c736416b0ffa1ed44ac9ee5d92'
            '996482e15d88af097f5e7a578885338cad5b8724f8abb4341749d5538801f5c0')
