# Maintainer; BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=apper
pkgver=0.7.0
pkgrel=2
pkgdesc="KDE tools for PackageKit"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Apper?content=84745"
license=('GPL')
depends=('kdebase-workspace' 'packagekit-qt2>=0.6.11')
makedepends=('cmake' 'automoc4')
provides=('kpackagekit')
conflicts=('kpackagekit')
replaces=('kpackagekit')
#source=("http://kde-apps.org/CONTENT/content-files/84745-${pkgname}-${pkgver}.tar.bz2")
#source=("http://dl.dropbox.com/u/37314029/${pkgname}-${pkgver}.tar.bz2")
source=("http://herecura.be/archlinux/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('4cf442a4f6e36c88f058c5bc9d782c404873950591205aa0043b1358a123f8cb')

build() {
	cd ${pkgname}-${pkgver}
	[ -d "build" ] && rm -rf build
	mkdir build
	cd build

	cmake ../ \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd ${pkgname}-${pkgver}
	cd build
	make DESTDIR=${pkgdir} install
}
