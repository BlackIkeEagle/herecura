# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_pkgname=chromaprint
pkgname=$_pkgname-libav
pkgver=1.1
pkgrel=1
pkgdesc='Library that implements a custom algorithm for extracting fingerprints from any audio source'
url='http://acoustid.org/chromaprint/'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libav')
makedepends=('cmake')
source=("https://bitbucket.org/acoustid/${_pkgname}/downloads/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('5a250f761761d2ce08e2591b9daa909393552939')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_EXAMPLES=ON \
		.
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
