# Maintainer: frio <development@frio.name>
pkgname=lxc-docker
pkgver=0.6.4
pkgrel=1
pkgdesc="pack, ship and run any application as a lightweight container"
arch=('i686' 'x86_64')
url="http://www.docker.io/"
license=('Apache License 2.0')
depends=('bridge-utils' 'iproute2' 'lxc')
makedepends=('go' 'git')
source=(
    "$pkgname::git://github.com/dotcloud/docker.git#tag=v$pkgver" 
    'docker.service'
)
sha256sums=('SKIP'
            '633f67629c9a49af7b203da736d39270586ebf5edf4c0a2641be7ff4d1623d0f')
conflicts=('lxc-docker-git')
options=(!strip)

build() {
  export CGO_ENABLED=0 # static linking
  export GOPATH="$srcdir/gopath"
  mkdir -p "$GOPATH"

  # make sure the build can find src/github.com/dotcloud/docker
  mkdir -p "${GOPATH}/src/github.com/dotcloud"
  ln -sf "$srcdir/$pkgname" "${GOPATH}/src/github.com/dotcloud/docker"

  export GOPATH="$GOPATH:$srcdir/$pkgname:$srcdir/$pkgname/vendor"

  [ -d build ] && rm -rf build
  mkdir build
  cd build
  go build -v -a github.com/dotcloud/docker/docker
}

package() {
  # install systemd service unit
  install -D -m 644 "$srcdir/docker.service" "$pkgdir/usr/lib/systemd/system/docker.service"

  # install docker binary
  install -D -m 755 "$srcdir/build/docker" "$pkgdir/usr/bin/docker"
}

# vim:set ts=2 sw=2 et:
