# Contributors : Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: BlackEagle < ike DOT devolder AT herecura DOT be >
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgname=xbmc-dharma
pkgver=33232
pkgrel=1
pkgdesc="XBMC Media Center Dharma branch"
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse')
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'LGPL')
depends=('lzo2' 'libxtst' 'jasper' 'libmysqlclient' 'libsamplerate' 'wavpack' 'libssh' 'bzip2' 
	'sdl_image' 'libva' 'dbus-core' 'libmms' 'glew' 'libmicrohttpd' 'libcdio' 'smbclient' 
	'libmad' 'libgl' 'fribidi' 'libmpeg2' 'faac' 'libmodplug' 'faad2' 'sdl_mixer' 'libass')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'subversion' 'unzip' 'zip'
	'vdpau-video'
	'libxrandr' 'libxinerama')
optdepends=(
	'lirc: remote controller support' 
	'udisks: automount external drives' 
	'upower: used to trigger suspend functionality' 
	'unrar: access compressed files without unpacking them'
	'vdpau-video: for nvidia hardware accelleration'
)
install=("${pkgname}.install")
source=( "FEH.sh" )
md5sums=('c3e2ab79b9965f1a4a048275d5f222c4')
noextract=()
options=(strip)

_svntrunk=http://xbmc.svn.sourceforge.net/svnroot/xbmc/branches/Dharma
_svnmod=XBMC
_prefix=/usr

build() {
	if [ -d $_svnmod/.svn ]; then
		msg2 "SVN tree found, reverting changes and updating to -r$pkgver"
		(cd $_svnmod && svn revert -R . ; svn up -r $pkgver) || return 1
	else
		msg2 "Checking out SVN tree of -r$pkgver"
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod || return 1
	fi

	# Configure XBMC
	#
	# Note on external-libs:
	#   - We cannot use external python because Arch's python was built with
	#     UCS2 unicode support, whereas xbmc expects UCS4 support
	#   - According to an xbmc dev using external/system ffmpeg with xbmc is "pure stupid" :D

	cd ${srcdir}

	# remove builddir if it already exists
	[ -d ${_svnmod}-build ] && rm -Rf ${_svnmod}-build

	# create builddir
	msg2 "create build directory"
	cp -a ${_svnmod} ${_svnmod}-build

	cd ${_svnmod}-build
	unset LDFLAGS; LDFLAGS="-Wl,--hash-style=gnu -Wl"

	msg2 "Bootstrapping XBMC"
	./bootstrap || return 1

	msg2 "Configuring XBMC" 
	./configure --prefix=${_prefix} --exec-prefix=${_prefix} \
		--disable-debug \
		--disable-optimizations \
		--enable-gl \
		--enable-vdpau \
		--enable-vaapi \
		--disable-profiling \
		--enable-joystick \
		--disable-xrandr \
		--disable-pulse \
		--disable-hal \
		--disable-avahi \
		--disable-external-ffmpeg \
		--disable-external-liba52 \
		--disable-external-libdts \
		--disable-external-python || return 1

	# Now (finally) build
	msg2 "Running make" 
	make || return 1
	make -C lib/addons/script.module.pil || return 1
	make -C lib/addons/script.module.pysqlite || return 1
}

package() {
	cd ${_svnmod}-build
	msg2 "Running make install" 
	make DESTDIR=${pkgdir} install || return 1

	#msg "bin/xbmc mods for ARCH package"
	#msg2 "Fixup init script lsb_release stuff to arch-release"
	#sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' \
		#${pkgdir}/usr/bin/xbmc || return 1
	#sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" \
		#${pkgdir}/usr/bin/xbmc || return 1

	# Replace FEH.py with FEH.sh (and thus remove external python dependency)
	msg2 "Replace FEH.py with FEH.sh (and thus remove external python dependency)"
	install -Dm755 ${srcdir}/FEH.sh \
		${pkgdir}${_prefix}/share/xbmc/FEH.sh || return 1
	sed -i -e 's/^python \(.*\)FEH.py \(.*\)$/\1FEH.sh \2/' \
		${pkgdir}${_prefix}/bin/xbmc || return 1

	# Tools
	msg2 "Tools"
	#install -Dm755 ${srcdir}/${_svnmod}-build/xbmc-xrandr \
		#${pkgdir}${_prefix}/lib/xbmc/xbmc-xrandr || return 1
	install -Dm755 ${srcdir}/${_svnmod}-build/tools/TexturePacker/TexturePacker \
		${pkgdir}${_prefix}/lib/xbmc/ || return 1

	# Licenses
	msg2 "Copy licenses"
	install -dm755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
	for licensef in LICENSE.GPL copying.txt; do
		mv ${pkgdir}${_prefix}/share/doc/xbmc/${licensef} \
			${pkgdir}${_prefix}/share/licenses/${pkgname} || return 1
	done

	# Docs
	msg2 "Copy docs"
	mv ${pkgdir}${_prefix}/share/doc/xbmc \
		${pkgdir}${_prefix}/share/doc/${pkgname} || return 1

	#cleanup some stuff
	msg2 "cleanup unneeded files"
	rm -rf ${pkgdir}/usr/share/xsessions
	rm -f ${pkgdir}/usr/share/xbmc/FEH.py

	# strip
	#msg "strip binaries"
	#find $pkgdir -type f -exec strip {} \; >/dev/null 2>/dev/null
}
