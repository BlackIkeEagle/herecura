# Maintainer: ruario 
# Contributor: RobertMe
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
pkgname=opera-devel # Set to opera if you want to install as your main/stable build
_bigrelease=11.01
_buildver=1175
_randomizer=tannen
pkgver=${_bigrelease}_${_buildver}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Alpha version."
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
provides=('opera')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
	    'gstreamer0.10-base-plugins: HTML5 open codecs support'
	    'gstreamer0.10-good: HTML5 open codecs support'
	    'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
	    'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=opera-devel.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/${_randomizer}_${_bigrelease}-${_buildver}/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
sha256sums=('e7f0bf38a54b9a59d96d6c38f66b1f6d6bc525cb1b66cefa5d09e3606870531d')
[ "$CARCH" = "x86_64" ] && sha256sums=('7190ef731f7746b1c7366e2ce220bf634a7324e7d64c82e3e93947dd2cf9cb5f')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
#_opdistro="Arch Linux"

build() {

	# Check if the $pkgname includes a suffix and if so set ${_suffix} for a suffix install.
	if echo $pkgname | grep -q "^opera-[a-zA-Z0-9]\{1,\}$"
	then
		_suffix="--suffix ${pkgname:6}"
	fi

	# 'Install' Opera into $pkgdir
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr ${_suffix} --repackage ${pkgdir}/usr
	install -D -m 644 ${pkgdir}/usr/share/${pkgname}/defaults/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p ${pkgdir}/usr/share/${pkgname}/custom/defaults
		echo "[ISP]" > ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		echo "Id=${_opdistro}" >> ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		chmod 644 ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
	fi
}
