# Maintainer: ruario 
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
pkgname=opera-devel
_bigrelease=11.01
_buildver=1160
_randomizer=minor
pkgver=${_bigrelease}_${_buildver}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Alpha version."
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
provides=('opera')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
	    'gstreamer0.10-base-plugins: HTML5 open codecs support'
	    'gstreamer0.10-good: HTML5 open codecs support'
	    'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
	    'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=${pkgname}.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/${_randomizer}_${_bigrelease}-${_buildver}/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
sha256sums=('8e1185e6601728d3801c02f9ee60fdc8b47923efc87c2efe27b8b8ba048bd5a4')
[ "$CARCH" = "x86_64" ] && sha256sums=('89c870c51281b4c3d69af8f428b7c0ed33784c827c11259ff76c8ce176fc8b1d')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
#_opdistro="Arch Linux"

build() {
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr --suffix devel --repackage ${pkgdir}/usr
	install -D -m 644 ${pkgdir}/usr/share/${pkgname}/defaults/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

	#Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p ${pkgdir}/usr/share/${pkgname}/custom/defaults
		echo "[ISP]" > ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		echo "Id=${_opdistro}" >> ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		chmod 644 ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
	fi
}
