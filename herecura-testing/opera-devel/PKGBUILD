# Maintainer: ruario 
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
pkgname=opera-devel
_bigrelease=11.00
_buildver=1136
_randomizer=partytime
pkgver=${_bigrelease}_${_buildver}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Alpha version."
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
provides=('opera')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
		'gstreamer0.10-base-plugins: HTML5 open codecs support'
		'gstreamer0.10-good: HTML5 open codecs support'
		'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
		'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=${pkgname}.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/${_randomizer}_${_bigrelease}-${_buildver}/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
sha256sums=('3051bc9ce83d7005ad194f3ef74fa1be1417e253a9fd77d55a32ad9dba5911e4')
[ "$CARCH" = "x86_64" ] && sha256sums=('12bc772786c731496fb2b849cfed528015d5fc24c9465f685c0c21ca4e606ea3')

build() {
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr --suffix devel --repackage ${pkgdir}/usr
	install -D -m 644 ${pkgdir}/usr/share/${pkgname}/defaults/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
