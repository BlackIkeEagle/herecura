# vim:set sw=2 sts=2 et:
# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: tobias [ tobias at archlinux org ]
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=vim
pkgname=(vim-beta-slim vim-beta-cli vim-beta-gvim vim-beta-rt)
#_basever=7.2
#_patchlevel=446
#pkgver=$_basever.$_patchlevel
pkgver=7.3f
pkgrel=1
_versiondir=vim${pkgver/./}
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
makedepends=('gpm' 'perl' 'python' 'ruby' 'lua' 'libxt' 'desktop-file-utils' 'gtk2'
             'gettext' 'pkgconfig' 'sed')
options=(force)
source=('vimrc' 'archlinux.vim' 'gvim.desktop' 'license.txt'
        "ftp://ftp.nluug.nl/pub/vim/unstable/unix/${pkgbase}-${pkgver}.tar.bz2")
#if [ $_patchlevel -gt 000 ]; then
  #for p in $(seq -w 001 $_patchlevel); do
    #source=( ${source[@]} ftp://ftp.nluug.nl/pub/vim/patches/$_basever/$_basever.$p)
  #done
#fi
sha256sums=('70623e05d612985c76048ee26cc3daf746bb0becf179bd33c976eb549ebb2399'
            'c4fe5e30dfd5093353e42c2c514d4492bec64da17f9a2b4e306af6ee39320715'
            '5f2d65e755424f688b990b20bce6bd84718b9d5f7944a5332b5dee72f09493f7'
            'bb4744930a0030085d382356e9fdd4f2049b6298147aee2470c7fca7ec82fd55'
            '55535266ec1e17971e215be99741e7cd3c830ea8554defa27b6305802ee109a0')

##### Build #####

build() {
  cd ${_versiondir}

  #msg2 'Patching...'

  #if [ $_patchlevel -gt 000 ]; then
    #for p in $srcdir/$_basever.*; do
      #msg "apply patch ${p}"
      #patch -p0 -i ${p} || return 1
    #done
  #fi

  cd ${srcdir}
  [ -d "vim-build" ] && rm -rf vim-build
  cp -a ${_versiondir} vim-build

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h

  msg2 'Building...'

  [ -d "gvim-build" ] && rm -rf gvim-build
  [ -d "vim-sl-build" ] && rm -rf vim-sl-build
  cp -a vim-build gvim-build
  cp -a vim-build vim-sl-build

  unset LDFLAGS; LDFLAGS="-Wl,--hash-style=gnu -Wl"

  msg2 'Building vim-slim'
  cd ${srcdir}/vim-sl-build
  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-compiledby=ArchLinux \
    --with-features=tiny --disable-gpm --enable-acl --with-x=no \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp=no --disable-pythoninterp \
    --disable-rubyinterp --enable-luainterp=no
  make

  msg2 'Building vim-cli'
  cd ${srcdir}/vim-build
  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-compiledby=ArchLinux \
    --with-features=huge --enable-gpm --enable-acl --with-x=no \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp=yes --enable-pythoninterp \
    --enable-rubyinterp --enable-luainterp=yes
  make

  msg2 'Building vim-gvim'
  cd ${srcdir}/gvim-build
  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-compiledby=ArchLinux \
    --with-features=huge --enable-gpm --enable-acl --with-x=yes \
    --enable-gui=gtk2 --enable-multibyte --enable-cscope \
    --enable-netbeans --enable-perlinterp=yes --enable-pythoninterp \
    --enable-rubyinterp --enable-luainterp=yes
  make
}

##### Packaging #####

package_vim-beta-slim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor, slim edition'
  conflicts=('vim')
  provides=('vim')

  cd ${srcdir}/vim-sl-build
  make VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r ${pkgdir}/usr/share/vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim-beta-slim
  install -Dm644 ${srcdir}/license.txt \
    ${pkgdir}/usr/share/licenses/vim-beta-slim/license.txt
}

package_vim-beta-cli() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor, cli only'
  depends=("vim-beta-rt=${pkgver}-${pkgrel}" 'gpm' 'perl' 'python' 'ruby' 'lua')
  conflicts=('vim')
  provides=('vim')

  cd ${srcdir}/vim-build
  make VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r ${pkgdir}/usr/share/vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim-beta-cli
  install -Dm644 ${srcdir}/license.txt \
    ${pkgdir}/usr/share/licenses/vim-beta-cli/license.txt
}

package_vim-beta-gvim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor (with advanced features, such as a GUI)'
  depends=('vim-beta-cli' 'libxt' 'desktop-file-utils' 'gtk2')
  conflicts=('gvim')
  provides=('gvim')
  install=gvim.install

  cd ${srcdir}/gvim-build
  make VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  # cleanup so there is no more conflict with vim-bin
  cd ${pkgdir}/usr/bin/
  mv vim gvim
  rm -f rview rvim vimdiff vimtutor xxd # all available in vim-bin
  rm -f eview evim gview gvimdiff rgview rgvim # symlinking to vim, has to be gvim
  ln -s gvim eview
  ln -s gvim evim
  ln -s gvim gview
  ln -s gvim gvimdiff
  ln -s gvim rgview
  ln -s gvim rgvim

  cd ${srcdir}/gvim-build

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f rview.1 rvim.1 vim.1 vimdiff.1 vimtutor.1 xxd.1 # provided by vim-bin
  done

  # Move the runtime for later packaging
  mv ${pkgdir}/usr/share/vim ${srcdir}/runtime-install

  # freedesktop links
  install -Dm644 ${srcdir}/gvim.desktop \
    ${pkgdir}/usr/share/applications/gvim.desktop
  install -Dm644 runtime/vim48x48.png \
    ${pkgdir}/usr/share/pixmaps/gvim.png

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim-beta-gvim
  install -Dm644 ${srcdir}/license.txt \
    ${pkgdir}/usr/share/licenses/vim-beta-gvim/license.txt
}

package_vim-beta-rt() {
  pkgdesc='Runtime for vim and gvim'
  backup=(etc/vimrc)
  conflicts=('vim-runtime')
  provides=('vim-runtime')

  # Install the runtime split from gvim
  install -dm755 ${pkgdir}/usr/share
  mv ${srcdir}/runtime-install ${pkgdir}/usr/share/vim

  # Don't forget logtalk.dict
  install -Dm644 ${srcdir}/gvim-build/runtime/ftplugin/logtalk.dict \
    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

  # rc files
  install -Dm644 ${srcdir}/vimrc ${pkgdir}/etc/vimrc
  install -Dm644 ${srcdir}/archlinux.vim \
    ${pkgdir}/usr/share/vim/vimfiles/archlinux.vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim-beta-rt
  install -Dm644 ${srcdir}/license.txt \
    ${pkgdir}/usr/share/licenses/vim-beta-rt/license.txt
}
