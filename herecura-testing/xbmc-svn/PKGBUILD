# Contributors : Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: BlackEagle < ike DOT devolder AT herecura DOT be >
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgname=xbmc-svn
pkgver=33134
pkgrel=1
pkgdesc="XBMC Media Center from SVN"
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse')
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'LGPL')
depends=(
	'unzip' # provides bzip2
	'lzo2' # provided by boost in makedeps but needed for TexturePacker
	'libmms'
	'libmad'
	# 'libssh' // COMMENTED BY TEST ! SUCCESS
	#'faad2' #provided by ffmpeg
	'libmpeg2'
	'wavpack'
	'libmodplug'
	'libmysqlclient'
	'fribidi'
	#'faac' #provided by ffmpeg # provides: libmp4v2
	'smbclient' # provides: tdb , talloc
	'libass' # provides: recode , enca , freetype2 , fontconfig , libpng
	'libsamplerate' # provides: libogg , libvorbis , flac , libsndfile , alsa-lib
	'libmicrohttpd' # provides: curl
	'libcdio' # provides: libcddb
	# common : xcb-proto , xproto , libxdmcp , libxau , libxcb , kbproto , libx11 , xextproto , libxext
	# sdl provides: all common and renderproto , libxrender
	'libxtst' # provides: all the same as sdl
	'sdl_mixer' # provides: all sdl and libogg , libvorbis , libmikmod , smpeg
	'sdl_image' # provides: all sdl and libpng , libjpeg , libtiff
	# 'libxrandr' # provides: all sdl and randrproto // mOVED TO MAKEDEPENDS
	'glew' # provides: all common and inputproto , libxi , libice , libxt , xf86vidmodeproto , libxxf86vm , libdrm , fixesproto , libxfixes , damageproto , libxdamage , libgl , dri2proto , mesa
	'jasper' # provides: all glew and freeglut , libxmu
	# 'libxinerama' // MOVED TO MAKEDEPENDS BY TEST ! SUCCESS
	'ffmpeg'
	#'libva' by ffmpeg
	#'libvpx' by ffmpeg
)

makedepends=(
	'boost'
	'cmake'
	'gperf'
	'nasm'
	'subversion'
	'zip'
	'cvs'
	#'libvdpau'
	'vdpau-video'
	'libxinerama'
	# checking for XINERAMA... no
	# configure: error: Could not find a required library. Please see the README for your platform.
	'libxrandr'
	'faad2' # ffmpeg has own aac decoder but xbmc still checks for libfaad
)
optdepends=(
	'gdb: for meaningful backtraces in case of trouble - STRONGLY RECOMMENDED' 
	'libssh: support for sshfs'
	'libva-sds: accelerated video playback for nvidia, ati/amd and some intel cards'
	'lirc: remote controller support' 
	'udisks: automount external drives' 
	'upower: used to trigger suspend functionality' 
	'unrar: access compressed files without unpacking them'
	'vdpau-video: for nvidia hardware accelleration'
)
install=("${pkgname}.install")
source=( "FEH.sh" )
md5sums=('c3e2ab79b9965f1a4a048275d5f222c4')
noextract=()
options=(strip)

_svntrunk=http://xbmc.svn.sourceforge.net/svnroot/xbmc/trunk
_svnmod=XBMC
_prefix=/usr

build() {
	cd ${srcdir}
	if [ -d $_svnmod/.svn ]; then
		msg2 "SVN tree found, reverting changes and updating to -r$pkgver"
		(cd $_svnmod && svn revert -R . ; svn up -r $pkgver) || return 1
	else
		msg2 "Checking out SVN tree of -r$pkgver"
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod || return 1
	fi

	# Configure XBMC
	#
	# Note on external-libs:
	#   - We cannot use external python because Arch's python was built with
	#     UCS2 unicode support, whereas xbmc expects UCS4 support
	#   - According to an xbmc dev using external/system ffmpeg with xbmc is "pure stupid" :D

	cd ${srcdir}
	if [ -d ${_svnmod}-build ]; then
		rm -Rf ${_svnmod}-build
	fi
	cp -a ${_svnmod} ${_svnmod}-build

	cd ${srcdir}/${_svnmod}-build

	msg2 "Bootstrapping XBMC"
	./bootstrap || return 1

	msg2 "Configuring XBMC" 
	./configure --prefix=${_prefix} --exec-prefix=${_prefix} \
		--enable-vdpau \
		--disable-pulse \
		--disable-debug \
		--disable-hal \
		--disable-avahi \
		--enable-external-libraries \
		--disable-external-ffmpeg \
		--disable-external-python || return 1

	# Now (finally) build
	msg2 "Running make" 
	make || return 1
	make -C lib/addons/script.module.pil || return 1
	make -C lib/addons/script.module.pysqlite || return 1
}

package() {
	cd "${srcdir}/${_svnmod}-build"
	msg2 "Running make install" 
	make DESTDIR=${pkgdir} install || return 1

	#msg "bin/xbmc mods for ARCH package"
	msg2 "Fixup init script lsb_release stuff to arch-release"
	sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' \
		${pkgdir}/usr/bin/xbmc || return 1
	sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" \
		${pkgdir}/usr/bin/xbmc || return 1

	# Replace FEH.py with FEH.sh (and thus remove external python dependency)
	msg2 "Replace FEH.py with FEH.sh (and thus remove external python dependency)"
	install -Dm755 ${srcdir}/FEH.sh \
		${pkgdir}${_prefix}/share/xbmc/FEH.sh || return 1
	sed -i -e 's/^python \(.*\)FEH.py \(.*\)$/\1FEH.sh \2/' \
		${pkgdir}${_prefix}/bin/xbmc || return 1

	# Tools
	msg2 "Tools"
	install -Dm755 ${srcdir}/${_svnmod}-build/xbmc-xrandr \
		${pkgdir}${_prefix}/lib/xbmc/xbmc-xrandr || return 1
	install -Dm755 ${srcdir}/${_svnmod}-build/tools/TexturePacker/TexturePacker \
		${pkgdir}${_prefix}/lib/xbmc/ || return 1

	# Licenses
	msg2 "Copy licenses"
	install -dm755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
	for licensef in LICENSE.GPL copying.txt; do
		mv ${pkgdir}${_prefix}/share/doc/xbmc/${licensef} \
			${pkgdir}${_prefix}/share/licenses/${pkgname} || return 1
	done

	# Docs
	msg2 "Copy docs"
	mv ${pkgdir}${_prefix}/share/doc/xbmc \
		${pkgdir}${_prefix}/share/doc/${pkgname} || return 1

	# cleanup some stuff
	msg2 "cleanup unneeded files"
	rm -rf ${pkgdir}/usr/share/xsessions
	rm -f ${pkgdir}/usr/share/xbmc/FEH.py

	# strip
	#msg "strip binaries"
	#find $pkgdir -type f -exec strip {} \; >/dev/null 2>/dev/null
}
