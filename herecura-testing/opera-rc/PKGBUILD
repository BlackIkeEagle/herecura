# Maintainer: ruario 
# Contributor: RobertMe
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
pkgname=opera-rc # Set to opera if you want to install as your main/stable build
_bigrelease=11.51
_buildver=1084
_randomizer=rc1
_rcver=1
pkgver=${_bigrelease}_rc${_rcver}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Release Candidate version."
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
	    'gstreamer0.10-base-plugins: HTML5 open codecs support'
	    'gstreamer0.10-good: HTML5 open codecs support'
	    'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
	    'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=opera-rc.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/${_randomizer}_${_bigrelease}-${_buildver}/Opera/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
md5sums=('598b48a3faed7df09cb58ef06885a393')
[ "$CARCH" = "x86_64" ] && md5sums=('7b2c69ec0b8e6bd9f33335ad82e82e6e')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
_opdistro="Arch Linux"

build() {

	# 'Install' Opera into $pkgdir
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr ${_suffix} --name ${pkgname} --repackage ${pkgdir}/usr
	install -D -m 644 ${pkgdir}/usr/share/${pkgname}/defaults/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p ${pkgdir}/usr/share/${pkgname}/custom/defaults
		echo "[ISP]" > ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		echo "Id=${_opdistro}" >> ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		chmod 644 ${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini
	fi
}
