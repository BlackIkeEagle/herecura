# Contributors : Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgname=xbmc-git
pkgver=20110825
pkgrel=1
pkgdesc="XBMC Media Center git master branch"
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse' 'xbmc-svn')
replaces=('xbmc-svn')
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'LGPL')
depends=('bzip2' 'faac' 'faad2' 'fribidi' 'glew' 'jasper' 'libass' 'libcdio' 'libgl' 'libmad' 'libmicrohttpd' 
         'libmodplug' 'libmpeg2' 'libmysqlclient' 'libsamplerate' 'libxtst' 'lzo2' 'python2' 'libplist'
         'sdl_image>=1.2.10' 'sdl_mixer' 'smbclient' 'unzip' 'wavpack' 'mesa-demos' 'xorg-xdpyinfo' 'yajl')
makedepends=('boost' 'cmake' 'git' 'gperf' 'nasm' 'libxinerama' 'libxrandr'
             # 'crystalhd-snapshot'
             'zip'
			 'libvdpau' 'libva')

optdepends=(
            'gdb: for meaningful backtraces in case of trouble - STRONGLY RECOMMENDED' 
            'libcrystalhd: Broadcom CrystalHD kernel module'
            'libssh: support for sshfs'
            'vdpau-video: accelerated video playback for nvidia cards' 
            'libva: accelerated video playback for nvidia, ati/amd and some intel cards'
            'lirc: remote controller support' 
            'pulseaudio: pulseaudio support'
            'udisks: automount external drives' 
            'upower: used to trigger suspend functionality' 
            'unrar: access compressed files without unpacking them')
install=("${pkgname}.install")
source=( "FEH.sh" )
sha256sums=('a7e5f2114fe19640fc24300b7c69686f37d63df901b2f95bfbbc6cf9ae1c3ede')
options=('strip' 'makeflags')

_prefix='/usr'

if [ -e .githash_${CARCH} ] ; then
	_gitphash=$(cat .githash_${CARCH})
else
	_gitphash=""
fi

_gitname='xbmc'
_gitroot='git://github.com/xbmc/xbmc.git'

build() {
	if [ -d ${srcdir}/${_gitname}/.git ] ; then
		( cd ${srcdir}/${_gitname} && git pull origin )
		msg "The local files are updated."
	else
		( git clone --depth 1 ${_gitroot} ${_gitname} )
	fi
	msg "GIT checkout done or server timeout"

	cd ${_gitname}
	[ "${_gitphash}" == $(git show | grep -m 1 commit | sed 's/commit //') ] && return 1

	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	cp -a ${_gitname} ${_gitname}-build

	msg "Starting make..."
	cd ${_gitname}-build

	# Configure XBMC
	#
	# Note on external-libs:
	#   - We cannot use external python because Arch's python was built with
	#     UCS2 unicode support, whereas xbmc expects UCS4 support
	#   - According to an xbmc dev using external/system ffmpeg with xbmc is "pure stupid" :D

    # fix lsb_release dependency
    sed -i -e 's:/usr/bin/lsb_release -d:cat /etc/arch-release:' xbmc/utils/SystemInfo.cpp

	# remove "--as-needed" from LDFLAGS
	unset LDFLAGS; LDFLAGS="-Wl,--hash-style=gnu"

	msg2 "Bootstrapping XBMC"
	./bootstrap

	msg2 "Configuring XBMC" 
    export PYTHON_VERSION=2  # external python v2
	./configure --prefix=${_prefix} --exec-prefix=${_prefix} \
		--enable-debug \
		--enable-optimizations \
		--enable-gl \
		--enable-vdpau \
		--enable-vaapi \
		--disable-profiling \
		--enable-joystick \
		--enable-external-libraries \
		--disable-external-ffmpeg \
		--disable-xrandr \
		--disable-pulse \
		--disable-hal \
		--disable-avahi

	# Now (finally) build
	msg2 "Running make" 
	make
}

package() {
	cd ${_gitname}-build
	msg2 "Running make install" 
	make DESTDIR=${pkgdir} install

	# Replace FEH.py with FEH.sh (and thus remove external python dependency)
	msg2 "Replace FEH.py with FEH.sh (and thus remove external python dependency)"
	install -Dm755 ${srcdir}/FEH.sh \
		${pkgdir}${_prefix}/share/xbmc/FEH.sh
	sed -i -e 's/^python \(.*\)FEH.py \(.*\)$/\1FEH.sh \2/' \
		${pkgdir}${_prefix}/bin/xbmc

	# Tools
	msg2 "Tools"
	install -Dm755 ${srcdir}/${_gitname}-build/tools/TexturePacker/TexturePacker \
		${pkgdir}${_prefix}/lib/xbmc/

	# Licenses
	msg2 "Copy licenses"
	install -dm755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
	for licensef in LICENSE.GPL copying.txt; do
		mv ${pkgdir}${_prefix}/share/doc/xbmc/${licensef} \
			${pkgdir}${_prefix}/share/licenses/${pkgname}
	done

	#cleanup some stuff
	msg2 "cleanup unneeded files"
	rm -rf ${pkgdir}/usr/share/xsessions
	rm -f ${pkgdir}/usr/share/xbmc/FEH.py
	# its not very usefull, and broken at the moment
	rm -f ${pkgdir}/usr/bin/xbmc-standalone
	# didnt we built --disable-xrandr ?
	rm -f ${pkgdir}/usr/lib/xbmc/xbmc-xrandr
	# remove icon theme cache
    rm -f ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache

	git show | grep -m 1 commit | sed 's/commit //' > ${startdir}/.githash_${CARCH}
}
