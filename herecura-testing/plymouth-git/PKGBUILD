# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: lh <jarryson#gmail.com>
pkgname=plymouth-git
pkgver=20100917
pkgrel=1
pkgdesc="A Linux boot screen implementation"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('initscripts' 'libdrm' 'gtk2')
makedepends=('git')
provides=('plymouth')
conflicts=('plymouth')
options=('!libtool' '!emptydirs')
install=plymouth.install
source=('plymouth-update-initrd'
	'plymouth-encrypt.initcpio_hook'
	'plymouth-encrypt.initcpio_install'
	'plymouth.functions'
	'plymouth.initcpio_hook'
	'plymouth.initcpio_install'
	'arch-logo.png'
	'system-release')
md5sums=('e59b4f04816fcbe506e9d4b725cb1e29'
         'f9418c98f84f948ccc860c08cf4177ed'
         'f218e7e4c6b27c5c2135a3de076d8b1a'
         '916fb026be911d4cf9c51d44bfee0791'
         '86c85dcf88dfdb6b895f72bdca1babd1'
         '3f7e1a50984788082e60a82931e1a21a'
         '9508a1adaac3641a0e6e6eb1155beb2f'
         'ab8a557d10f74bec5c94ed6cae34729c')


if [ -e .githash ] ; then
	_gitphash=$(cat .githash)
else
	_gitphash=""
fi
_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'
_gitbranch='master'

build() {
	msg "Connecting to git.freedesktop.org GIT server...."
	if [ -d ${_gitname} ] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
	fi
	msg "GIT checkout done or server timeout. Preparing sources..."

	cd ${srcdir}/${_gitname}
	[ "${_gitphash}" == $(git show | grep -m 1 commit | sed 's/commit //') ] && return 1

	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build

	git clone ${_gitname} ${_gitname}-build

	msg "Starting make..."
	cd ${srcdir}/${_gitname}-build

	sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' -i src/libply-splash-graphics/ply-image.c
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
		--enable-tracing \
		--enable-tests \
		--with-system-root-install \
		--disable-tests \
		--enable-gdm-transition \
		--without-rhgb-compat-link \
		--with-logo=/usr/share/plymouth/arch-logo.png \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D

	make
}

package() {
	cd ${_gitname}-build
	make DESTDIR=$pkgdir install

	cd $pkgdir

	find . -name "*.orig" -delete

	cd ${srcdir}/${_gitname}-build
	install -Dm644 $srcdir/plymouth.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth
	install -Dm644 $srcdir/plymouth.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth
	install -Dm644 $srcdir/plymouth-encrypt.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth-encrypt
	install -Dm644 $srcdir/plymouth-encrypt.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth-encrypt
	install -Dm644 $srcdir/plymouth.functions ${pkgdir}/etc/rc.d/functions.d/plymouth.functions
	install -Dm644 $srcdir/arch-logo.png ${pkgdir}/usr/share/plymouth/arch-logo.png
	install -Dm644 $srcdir/system-release ${pkgdir}/etc/system-release
	git show | grep -m 1 commit | sed 's/commit //' > ${startdir}/.githash
}

