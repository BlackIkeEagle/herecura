# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
pkgbase="kernel26-slk-rc"
pkgname=('kernel26-slk-rc' 'kernel26-slk-rc-headers')
# pkgname=kernel26-custom       # Build kernel with a different name
_kernelname=-slk-rc
_basekernel=2.6.36
pkgver=${_basekernel}_rc7
pkgrel=1
arch=(i686 x86_64)
license=('GPL2')
url="http://www.kernel.org"
source=(
#ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_basekernel.tar.bz2
ftp://ftp.kernel.org/pub/linux/kernel/v2.6/testing/linux-${pkgver/_/-}.tar.bz2
		# the main kernel config files
		config-${_basekernel}.i686
		config-${_basekernel}.x86_64
		# standard config files for mkinitcpio ramdisk
		${pkgname}.preset
		# special patches for some longtime bug
)
sha256sums=('d8268d54844bcb847fb0963d03fc9cba0c6e8226a28ea71556ac374a8989d5b3'
            'dbc96ec604474cbe25bba8e056e164ab517693fd1e79d9271b428727d35f7ef1'
            'd688121743958b3b8e94ebe0f5edadaa76db79b4e30dce38a1730b98272019c1'
            '2b5abb325d33a02bb1d21a8b4d314a9e155ce41a066ad94c8a95b01341886862')

build() {
	cd ${srcdir}/linux-${pkgver/_/-}
	return 1 # make sure it won't build

	if [ "$CARCH" = "x86_64" ]; then
		cat ../config-${_basekernel}.x86_64 >./.config
	else
		cat ../config-${_basekernel}.i686 >./.config
	fi
	if [ "${_kernelname}" != "" ]; then
		sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"\U${_kernelname}\"|g" ./.config
	fi
	sed -i -e 's/^EXTRAVERSION =.*/EXTRAVERSION =/' ./Makefile

	# hack to prevent output kernel from being marked as dirty or git
	echo "" > ${srcdir}/linux-${pkgver/_/-}/.scmversion

	# get kernel version
	make prepare
	# load configuration
	# Configure the kernel. Replace the line below with one of your choice.
	#make menuconfig # CLI menu for configuration
	#make xconfig # X-based configuration
	#make oldconfig # using old config from previous kernel version
	# ... or manually edit .config
	####################
	# stop here
	# this is useful to configure the kernel
	#msg "Stopping build"
	#return 1
	####################
	yes "" | make config
	# build!
	make bzImage modules
}

package_kernel26-slk-rc() {
	pkgdesc="The Linux Kernel and modules, inspired by Slackware kernel"
	groups=('base')
	provides=('kernel26')
	backup=(etc/mkinitcpio.d/${pkgname}.preset)
	depends=('coreutils' 'module-init-tools' 'mkinitcpio>=0.5.20')
	# pwc, ieee80211 and hostap-driver26 modules are included in kernel26 now
	# nforce package support was abandoned by nvidia, kernel modules should cover everything now.
	# kernel24 support is dropped since glibc24
	replaces=('kernel24' 'kernel24-scsi' 'kernel26-scsi'
	'alsa-driver' 'ieee80211' 'hostap-driver26'
	'pwc' 'nforce' 'squashfs' 'unionfs' 'ivtv'
	'zd1211' 'kvm-modules' 'iwlwifi' 'rt2x00-cvs'
	'gspcav1' 'atl2' 'wlan-ng26' 'rt2500' 'nouveau-drm')
	install=${pkgname}.install
	optdepends=('crda: to set the correct wireless channels of your country'
	'linux-firmware: when having some hardware needing special firmware')

	KARCH=x86
	cd ${srcdir}/linux-${pkgver/_/-}
	# get kernel version
	_kernver="$(make kernelrelease)"
	mkdir -p ${pkgdir}/{lib/modules,boot}
	make INSTALL_MOD_PATH=${pkgdir} modules_install
	cp System.map ${pkgdir}/boot/System.map26${_kernelname}
	cp arch/$KARCH/boot/bzImage ${pkgdir}/boot/vmlinuz26${_kernelname}

	# install fallback mkinitcpio.conf file and preset file for kernel
	install -m644 -D ${srcdir}/${pkgname}.preset ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset
	# set correct depmod command for install
	sed \
		-e  "s/KERNEL_NAME=.*/KERNEL_NAME=${_kernelname}/g" \
		-e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
		-i $startdir/${pkgname}.install
	sed \
		-e "s|source .*|source /etc/mkinitcpio.d/kernel26${_kernelname}.kver|g" \
		-e "s|default_image=.*|default_image=\"/boot/${pkgname}.img\"|g" \
		-e "s|fallback_image=.*|fallback_image=\"/boot/${pkgname}-fallback.img\"|g" \
		-i ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset

	echo -e "# DO NOT EDIT THIS FILE\nALL_kver='${_kernver}'" > ${pkgdir}/etc/mkinitcpio.d/${pkgname}.kver
	# remove build and source links
	rm -f ${pkgdir}/lib/modules/${_kernver}/{source,build}
	# remove the firmware
	rm -rf ${pkgdir}/lib/firmware
}

package_kernel26-slk-rc-headers() {
	pkgdesc="Header files and scripts for building modules for kernel26-slk"
	provides=('kernel26-headers')
	mkdir -p ${pkgdir}/lib/modules/${_kernver}
	cd ${pkgdir}/lib/modules/${_kernver}
	ln -sf ../../../usr/src/linux-${_kernver} build
	cd ${srcdir}/linux-${pkgver/_/-}
	install -D -m644 Makefile \
		${pkgdir}/usr/src/linux-${_kernver}/Makefile
	install -D -m644 kernel/Makefile \
		${pkgdir}/usr/src/linux-${_kernver}/kernel/Makefile
	install -D -m644 .config \
		${pkgdir}/usr/src/linux-${_kernver}/.config

	# copy files necessary for later builds, like nvidia and vmware
	cp Module.symvers ${pkgdir}/usr/src/linux-${_kernver}
	cp -a scripts ${pkgdir}/usr/src/linux-${_kernver}
	# fix permissions on scripts dir
	chmod og-w -R ${pkgdir}/usr/src/linux-${_kernver}/scripts
	mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/.tmp_versions

	mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel

	cp arch/$KARCH/Makefile ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
	if [ "$CARCH" = "i686" ]; then
		cp arch/$KARCH/Makefile_32.cpu ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
	fi
	cp arch/$KARCH/kernel/asm-offsets.s ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel/

	# add docbook makefile
	install -D -m644 Documentation/DocBook/Makefile \
		${pkgdir}/usr/src/linux-${_kernver}/Documentation/DocBook/Makefile

	# add config
	for config in `find ./include/config -size +1c -type f`; do
		mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/$(dirname ${config})
		cp -a ${config} ${pkgdir}/usr/src/linux-${_kernver}/$(dirname ${config})
	done

	# add headers
	for header in `find -size +1c -name '*.h'`; do
		mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/$(dirname ${header})
		cp -a ${header} ${pkgdir}/usr/src/linux-${_kernver}/$(dirname ${header})
	done

	# copy in Kconfig files
	for i in `find . -name "Kconfig*"`; do
		mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/`echo $i | sed 's|/Kconfig.*||'`
		cp $i ${pkgdir}/usr/src/linux-${_kernver}/$i
	done

	chown -R root.root ${pkgdir}/usr/src/linux-${_kernver}
	find ${pkgdir}/usr/src/linux-${_kernver} -type d -exec chmod 755 {} \;
	# remove unneeded architectures
	rm -rf ${pkgdir}/usr/src/linux-${_kernver}/arch/{alpha,arm,arm26,avr32,blackfin,cris,frv,h8300,ia64,m32r,m68k,m68knommu,mips,microblaze,mn10300,parisc,powerpc,ppc,s390,score,sh,sh64,sparc,sparc64,tile,um,v850,xtensa}
}
