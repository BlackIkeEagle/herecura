# Contributor: Adria Arrufat <swiftscythe@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Daniel Hill <daniel.hill@orcon.net.nz>

pkgname=clementine-git
pkgver=20110920
pkgrel=1
pkgdesc="A modern music player and library organiser and a port of Amarok 1.4, with some features rewritten to take advantage of Qt4."
arch=('i686' 'x86_64')
url="http://code.google.com/p/clementine-player/"
license=('GPL')
depends=('gstreamer0.10-base' 'taglib' 'glew' 'liblastfm' 'libgpod' 'libmtp' 'hicolor-icon-theme')
makedepends=('git' 'boost' 'cmake')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
provides=('clementine')
conflicts=('clementine' 'clementine-svn')
replaces=('clementine-svn')
install=(${pkgname}.install)
source=()
md5sums=()

if [ -e .githash_${CARCH} ] ; then
	_gitphash=$(cat .githash_${CARCH})
else
	_gitphash=""
fi

_gitname="clementine"
_gitroot="https://code.google.com/p/clementine-player/"

build() {
	if [ -d ${srcdir}/${_gitname}/.git ] ; then
		( cd ${srcdir}/${_gitname} && git pull origin )
		msg "The local files are updated."
	else
		( git clone ${_gitroot} ${_gitname} )
	fi
	msg "GIT checkout done or server timeout"

	cd ${_gitname}
	if [ "${_gitphash}" == $(git show | grep -m 1 commit | sed 's/commit //') ]; then
		msg "Git hash is the same as previous build"
		return 1
	fi

	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	cp -a ${_gitname} ${_gitname}-build

	msg "Starting make..."
	cd ${_gitname}-build
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd ${_gitname}-build
	make DESTDIR=${pkgdir} install
	git show | grep -m 1 commit | sed 's/commit //' > ${startdir}/.githash_${CARCH}
}
