#Maintainer: Mathias Stearn <mathias@10gen.com>
#Contributor: Alec Thomas
pkgname=mongodb
pkgver=1.6.3
pkgrel=1
pkgdesc="A high-performance, open source, schema-free document-oriented database."
arch=('i686' 'x86_64')
url="http://www.mongodb.org"
license=('AGPL')
makedepends=(gcc scons boost)
depends=(boost-libs spidermonkey pcre)
optdepends=('libpcap: needed for mongosniff')
backup=('etc/conf.d/mongodb' 'etc/mongodb.conf')
source=("http://downloads.mongodb.org/src/${pkgname}-src-r${pkgver}.tar.gz"
        "mongodb.rc"
        "mongodb.conf")
sha256sums=('3c4e567ca6e07fa96f8025402803dbc077aa4d3e50b6decb99e06de5dd117c90'
            '26b7f46902a822d7fa0b4219f1e68cbe0d0ac1d0e62c37fd8fd081dd4c88d70c'
            'f3fc037c704d67c68c7d69c46878af3d8a601e48ca51105835f740cddb1b2b2e')
install="mongodb.install"

build() {
    local SCONSFLAGS="$MAKEFLAGS"

    cd "${pkgname}-src-r${pkgver}"

    scons --pch all --full || return 1
}

package() {
    cd "${pkgname}-src-r${pkgver}"

    scons --pch install --full --prefix="$pkgdir/usr" || return 1

    install -Dm 755 "$startdir/mongodb.rc" "$pkgdir/etc/rc.d/mongodb"
    install -Dm 644 "$startdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
    install -dm 700 "$pkgdir/var/state/mongodb"

    if [ -d "$pkgdir/usr/lib64" ]
    then
        mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
    fi
}
