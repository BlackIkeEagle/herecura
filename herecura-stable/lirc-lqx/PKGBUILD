# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Paul Mattal <paul@archlinux.org>

pkgbase='lirc-lqx'
pkgname=('lirc-lqx' 'lirc-lqx-utils')
pkgver=0.8.7
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
_kernver=2.6.36-LQX
makedepends=('kernel26-lqx>=2.6.36' 'kernel26-lqx<2.6.37' 'kernel26-lqx-headers>=2.6.36' 'kernel26-lqx-headers<2.6.37'
	'help2man' 'alsa-lib' 'libx11' 'libsm' 'python2' 'libftdi')
replaces=('lirc+pctv')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/lirc/lirc-${pkgver}.tar.bz2"
	'lircd'
	'lircmd'
	'lirc.logrotate'
	'lircd.conf'
	'irexec.conf'
	'irexecd')

build() {
	cd lirc-${pkgver}

	# Disabling lirc_gpio driver as it does no longer work Kernel 2.6.22+
	sed -i -e "s:lirc_gpio\.o::" drivers/lirc_gpio/Makefile.am

	autoreconf
	libtoolize

	./configure --enable-sandboxed \
		--prefix=/usr \
		--with-driver=all \
		--with-kerneldir=/usr/src/linux-${_kernver} \
		--with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
		--with-transmitter

	# disable parallel and bt829
	# because of incompatibility with smp systems
	sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile

	make
}

package_lirc-lqx() {
	pkgdesc="Linux Infrared Remote Control kernel modules for SLK kernel"
	depends=("lirc-lqx-utils>=${pkgver}" 'kernel26-lqx>=2.6.36' 'kernel26-lqx<2.6.37')
	install=lirc.install
	provides=('lirc')

	cd lirc-${pkgver}
	cd drivers
	make DESTDIR=${pkgdir} install

	# set the kernel version in install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
		${startdir}/lirc.install
}

package_lirc-lqx-utils() {
	pkgdesc="Linux Infrared Remote Control utils for SLK kernel"
	depends=('alsa-lib' 'libx11' 'libsm' 'python2' 'libftdi')
	backup=('etc/conf.d/lircd.conf'
		'etc/conf.d/irexec.conf')
	provides=('lirc-utils')

	cd lirc-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}/usr/share/lirc" "${pkgdir}/etc/rc.d"
	cp "${srcdir}"/{lircd,lircmd,irexecd} "${pkgdir}/etc/rc.d"
	cp -rp remotes "${pkgdir}/usr/share/lirc"
	chmod -R go-w "${pkgdir}/usr/share/lirc/"

	# install the logrotate config
	install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"

	# install conf.d file
	install -Dm644 "${srcdir}/lircd.conf" "${pkgdir}/etc/conf.d/lircd.conf"

	# install conf.d file
	install -Dm644 "${srcdir}/irexec.conf" "${pkgdir}/etc/conf.d/irexec.conf"

	install -d "${pkgdir}/etc/lirc"

	# remove built modules
	rm -r "${pkgdir}/lib/"
}
