# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Paul Mattal <paul@archlinux.org>

pkgbase='lirc-bemm'
pkgname=('lirc-bemm' 'lirc-bemm-utils')
pkgver=0.9.0
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
_kernver=2.6.38-BEMM
makedepends=('kernel26-bemm>=2.6.38' 'kernel26-bemm<2.6.39' 'kernel26-bemm-headers>=2.6.38' 'kernel26-bemm-headers<2.6.39'
	'help2man' 'alsa-lib' 'libx11' 'libsm' 'python2' 'libftdi' 'libirman')
replaces=('lirc+pctv')
options=('!libtool' '!makeflags')
source=(
	"http://downloads.sourceforge.net/lirc/lirc-${pkgver}.tar.bz2"
	'lircd'
	'lircmd'
	'lirc.logrotate'
	'lircd.conf'
	'irexec.conf'
	'irexecd'
)
sha256sums=(
	'6323afae6ad498d4369675f77ec3dbb680fe661bea586aa296e67f2e2daba4ff'
	'16e1285eb473a8e15796bc828dc11eb88a1007f30d2eb35727239a50859a7b34'
	'111f8bd4b69e7caa6d2b9da87938f949a5fcba01319c1611decbd51caf0ff497'
	'bd13ca00e30d85ff9166c03b8f7a20195ef89794e66d7e54f04ba1d014a73e7d'
	'a64962f310805db250e574464fce93cb4566a6f5bac1d9aad462435090bf3cb2'
	'398b9867c22537e71ac80e2e20a6915c52d90a8e542c1efaef6b8608a54e10ed'
	'175b4ce902688885b40423f2a55b3f55ccf54b55582f1fbd51a4cfcdc7a0cfb9'
)

build() {
	cd lirc-${pkgver}

	# Disabling lirc_gpio driver as it does no longer work Kernel 2.6.22+
	#sed -i -e "s:lirc_gpio\.o::" drivers/lirc_gpio/Makefile.am

  sed -i '/AC_PATH_XTRA/d' configure.ac
  sed -e 's/@X_CFLAGS@//g' \
      -e 's/@X_LIBS@//g' \
      -e 's/@X_PRE_LIBS@//g' \
      -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am

	autoreconf
	libtoolize

	PYTHON=python2 ./configure --enable-sandboxed \
		--prefix=/usr \
		--with-driver=all \
		--with-kerneldir=/usr/src/linux-${_kernver} \
		--with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
		--with-transmitter

	# disable parallel and bt829
	# because of incompatibility with smp systems
#	sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
#		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile

  # Remove drivers already in kernel
  sed -e "s:lirc_dev::" -e "s:lirc_bt829::" -e "s:lirc_igorplugusb::" \
      -e "s:lirc_imon::" -e "s:lirc_parallel::" -e "s:lirc_sasem::" \
      -e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
      -i Makefile drivers/Makefile drivers/*/Makefile tools/Makefile 

	make
}

package_lirc-bemm() {
	pkgdesc="Linux Infrared Remote Control kernel modules for BEMM kernel"
	depends=("lirc-bemm-utils>=${pkgver}" 'kernel26-bemm>=2.6.38' 'kernel26-bemm<2.6.39')
	install=lirc.install
	provides=('lirc')

	cd lirc-${pkgver}
	cd drivers
	make DESTDIR=${pkgdir} install

	# gzip all modules
	find ${pkgdir} -name '*.ko' -exec gzip -9 {} \;

	# set the kernel version in install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
		${startdir}/lirc.install
}

package_lirc-bemm-utils() {
	pkgdesc="Linux Infrared Remote Control utils for BEMM kernel"
	depends=('alsa-lib' 'libx11' 'libftdi' 'libirman')
	optdepends=('python2: pronto2lirc util')
	backup=('etc/conf.d/lircd.conf'
		'etc/conf.d/irexec.conf')
	provides=('lirc-utils')

	cd lirc-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}/usr/share/lirc" "${pkgdir}/etc/rc.d"
	cp "${srcdir}"/{lircd,lircmd,irexecd} "${pkgdir}/etc/rc.d"
	cp -rp remotes "${pkgdir}/usr/share/lirc"
	chmod -R go-w "${pkgdir}/usr/share/lirc/"

	# install the logrotate config
	install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"

	# install conf.d file
	install -Dm644 "${srcdir}/lircd.conf" "${pkgdir}/etc/conf.d/lircd.conf"

	# install conf.d file
	install -Dm644 "${srcdir}/irexec.conf" "${pkgdir}/etc/conf.d/irexec.conf"

	install -d "${pkgdir}/etc/lirc"

	# remove built modules
	rm -r "${pkgdir}/lib/"
}
