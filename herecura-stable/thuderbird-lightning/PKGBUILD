pkgname=thunderbird-lightning
pkgver=1.0b2
pkgrel=1
_tb_ver=3.1
_tb_libdir=thunderbird-3.1 # Adjust this to your thunderbird directory in /usr/lib
_comm_dir=comm-1.9.2
pkgdesc="An integrated calendar extension for Mozilla Thunderbird"
arch=('i686' 'x86_64')
url="http://www.mozilla.org/projects/calendar/lightning/"
license=('MPL' 'GPL' 'LGPL')
depends=('thunderbird' 'xulrunner')
makedepends=('pkgconfig' 'zip' 'glib' 'libidl2' 'wireless_tools' 'python2')
options=('!ccache' '!distcc' '!makeflags')
source=(
	"http://releases.mozilla.org/pub/mozilla.org/calendar/lightning/releases/1.0b2rc3/source/lightning-$pkgver.source.tar.bz2"
	"lightning-libs.patch"
	"lib-google-breakpad.patch"
)
sha256sums=(
	'887f8d49e6dd36478178b8438f28252370169e60a39383628c0862d8881eb9b2'
	'42463265f1ccd144c01802a4c452937bd100ec49baad851e91b6a97b00d4d6ae'
	'44ad8cc11ab73397b0bff1cdc7813c8196383a6f695a0a6c28aea432e5ac5523'
)

build() {
  # Compile needed parts of Thunderbird
  cd $srcdir/$_comm_dir

  patch -p0 -i $srcdir/lightning-libs.patch || return 1
  patch -p1 -i $srcdir/lib-google-breakpad.patch || return 1

  sed -i -e 's#python$#python2#' `find -name *.py` || return 1
  export PYTHON=python2

  ./configure --enable-application=mail --enable-calendar || return 1
  make || return 1

  # Install to the Thunderbird lib directory
  # If someone knows a better way to extract the em:id, please let me know :)
  _emid=`grep em:id $srcdir/comm-1.9.2/mozilla/dist/xpi-stage/lightning/install.rdf | tail -n1 | sed 's/.*>\(.*\)<.*/\1/'`
  mkdir -p $pkgdir/usr/lib/$_tb_libdir/extensions/$_emid
  cd $pkgdir/usr/lib/$_tb_libdir/extensions/$_emid
  # Look for the current (highest) version number of the XPI
  bsdtar -x -f $srcdir/comm-1.9.2/mozilla/dist/xpi-stage/lightning.xpi || return 1

  # Fix permissions
  find -type d -exec chmod 0755 \{\} \+ || return 1
  find -type f -exec chmod 0644 \{\} \+ || return 1
  find -name '*.so' -exec chmod 0755 \{\} \+
}
