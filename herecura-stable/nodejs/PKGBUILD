# Maintainer: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>
pkgname=nodejs
pkgver=0.4.2
pkgrel=1
pkgdesc="Evented I/O for V8 javascript"
arch=('i686' 'x86_64')
url="http://nodejs.org/"
license=('MIT')
depends=('python2')
optdepends=('openssl: TLS support')
conflicts=('nodejs-unstable')
source=("http://nodejs.org/dist/node-v$pkgver.tar.gz")
sha256sums=('09b1100ca6828eedbe52418fbeb3352d71c0b1ff3344c44a5af3efb80c5b908c')

build() {
  cd "node-v$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print) wscript tools/waf-light tools/node-waf; do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' "$file"
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' "$file"
  done
  sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
  sed -i "s|python |python2 |" Makefile

  ./configure --prefix=/usr
  make
}

package() {
  cd "node-v$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
