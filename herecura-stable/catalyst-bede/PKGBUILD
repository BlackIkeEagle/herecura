# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Contributor: aidanlinz
# Contributor: Spasswolf
# Contributor: DestroyFX
# Contributor: Rip-Rip

_kernver='2.6.38-BEDE'

pkgname=catalyst-bede
pkgver=11.3
pkgrel=1
pkgdesc="AMD/ATI Catalyst drivers for kernel26-bede. fglrx kernel module only"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('kernel26-bede>=2.6.38' 'kernel26-bede<2.6.39' "catalyst-utils=${pkgver}")
makedepends=('kernel26-bede-headers>=2.6.38' 'kernel26-bede-headers<2.6.39')
conflicts=('catalyst-test' 'nvidia' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri')
install=${pkgname}.install

source=(
	http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run
	makefile_compat.patch
	2.6.38_console.patch
	no_bkl.patch
)
sha256sums=(
	'e0bcb7e6c12ff1961c057df58fba396ab59356aaebbb1f5dd508c40b8610ed5e'
	'd344df75ffbe9bd899e288862f41983c2e24b5eff3e7e8cf02d4891eeb137fff'
	'46ae66c3985ce3af21e36d3bd71019a6b81e2884099253e2772384bd67a77c24'
	'c4031cd239edb732186f5a51442d2154218d822871d4eb3e86eaeaaff1b68ecc'
)

build() {
	## Unpack archive
	msg "Unpacking archive ..."
	/bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files

	if [ "${CARCH}" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	cd "${srcdir}/archive_files/"

	patch -Np1 -i ${srcdir}/makefile_compat.patch

    patch -Np1 -i ${srcdir}/2.6.38_console.patch

    if [ `cat /lib/modules/${_kernver}/build/.config | grep -c CONFIG_BKL=y` = 0 ]; then
		patch -Np1 -i ${srcdir}/no_bkl.patch
    fi

	###--- cut ---### ati's code from their make.sh file

	# ==============================================================
	# resolve if we are running a SMP enabled kernel
	def_smp=-D__SMP__

	# ==============================================================
	# resolve whether we need to set PAGE_ATTR_FIX 
	PAGE_ATTR_FIX=0

	# ==============================================================
	# resolve if we are running a MODVERSIONS enabled kernel

	def_modversions=''

	# ==============================================================
	# resolve if we are building for a kernel with a fix for CVE-2010-3081
	# On kernels with the fix, use arch_compat_alloc_user_space instead
	# of compat_alloc_user_space since the latter is GPL-only

	COMPAT_ALLOC_USER_SPACE=arch_compat_alloc_user_space

	###--- cut ---###


	cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" ./
	cp 2.6.x/Makefile ./


	## Compiling module
	make -C /lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
		MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
		PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}

package() {
	cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "${pkgdir}/lib/modules/${_kernver}/video/"
	install -m644 fglrx.ko "${pkgdir}/lib/modules/${_kernver}/video/"
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"

	# gzip all modules
	find ${pkgdir} -name '*.ko' -exec gzip -9 {} \;

	## License
	install -m644 "${srcdir}/archive_files/ATI_LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"

	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
}
