# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Contributor: aidanlinz
# Contributor: Spasswolf
# Contributor: DestroyFX
# Contributor: Rip-Rip

_kernver='2.6.39-BEDE'

pkgname=catalyst-bede
pkgver=11.6
pkgrel=2
pkgdesc="AMD/ATI Catalyst drivers for kernel26-bede. fglrx kernel module only"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('kernel26-bede>=2.6.39' 'kernel26-bede<2.6.40' "catalyst-utils=${pkgver}")
makedepends=('kernel26-bede-headers>=2.6.39' 'kernel26-bede-headers<2.6.40')
conflicts=('catalyst-test' 'nvidia' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri')
provides=('catalyst')
install=${pkgname}.install

source=(
	http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run
	makefile_compat.patch
	no_bkl.patch
	2.6.39_bkl.patch
)

md5sums=('516ea47b13d367e09a90acb9422df738'
	 '3e1b82bd69774ea808da69c983d6a43b'
	 '6a6b2133aa6ef3bcd377731b19c0553a'
	 'eb24250f8e537273bae9dbd79a022d61')

build() {
	## Unpack archive
	msg "Unpacking archive ..."
	/bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files

	if [ "${CARCH}" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	cd "${srcdir}/archive_files/"

	patch -Np1 -i ${srcdir}/makefile_compat.patch

    if [ `cat /lib/modules/${_kernver}/build/.config | grep -c CONFIG_BKL=y` = 0 ]; then
		patch -Np1 -i ${srcdir}/no_bkl.patch
        patch -Np1 -i ${srcdir}/2.6.39_bkl.patch
    fi

	###--- cut ---### ati's code from their make.sh file

	# ==============================================================
	# resolve if we are running a SMP enabled kernel
	def_smp=-D__SMP__

	# ==============================================================
	# resolve whether we need to set PAGE_ATTR_FIX 
	PAGE_ATTR_FIX=0

	# ==============================================================
	# resolve if we are running a MODVERSIONS enabled kernel

	def_modversions=''

	# ==============================================================
	# resolve if we are building for a kernel with a fix for CVE-2010-3081
	# On kernels with the fix, use arch_compat_alloc_user_space instead
	# of compat_alloc_user_space since the latter is GPL-only

	COMPAT_ALLOC_USER_SPACE=arch_compat_alloc_user_space

	###--- cut ---###


	cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" ./
	cp 2.6.x/Makefile ./


	## Compiling module
	make -C /lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
		MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
		PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}

package() {
	cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "${pkgdir}/lib/modules/${_kernver}/video/"
	install -m644 fglrx.ko "${pkgdir}/lib/modules/${_kernver}/video/"

	## License
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "${srcdir}/archive_files/ATI_LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/"

	# gzip all modules
	find ${pkgdir} -name '*.ko' -exec gzip -9 {} \;

	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
}
