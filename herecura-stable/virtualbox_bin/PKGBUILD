# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: xduugu <xduuguΘgmx·com>
# Contributor: Peter 'piie' Feuerer <peterΘpiie·net>
# Contributor: Sascha Pfau <MrPeacockΘgmail·com>
# Contributor: iggy <iggy.mfΘgmail·com>

pkgname="virtualbox_bin"
pkgver="3.2.8"
_build="64453"
pkgrel=2
pkgdesc="Powerful x86 virtualization (Personal Use Binaries Edition)."
arch=('i686' 'x86_64')
url="http://virtualbox.org"
license=('custom:PUEL')
depends=('libidl2' 'libxcursor' 'libxinerama' 'libxslt' 'curl' 'gcc' 'make' 'kernel26-headers')
optdepends=("qt: for VirtualBox GUI"
	"sdl: for VBoxSDL and VirtualBox GUI"
	"libgl: for shared OpenGL"
	"libxt: for shared Clipboard"
	"alsa-lib: for ALSA support"
	"pulseaudio: for PulseAudio support")
provides=("virtualbox=${pkgver}")
conflicts=('virtualbox-ose' 'virtualbox-modules')
install='virtualbox.install'
_arch='x86'
[ "$CARCH" = "x86_64" ] && _arch='amd64'
source=("VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run::http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run"
	'10-vboxdrv.rules'
	'vboxdrv'
	'virtualbox-unlocked_ioctl.patch')
sha256sums=('e1de3139499397b33da68f3a0e6941d50638206648b432e1a86de5cbff5171f9'
	'f2123c40b37be9144eaa974f90c1708e61101d69a3a31aeade92746e07bf9bb7'
	'85637e28829c515ad0538a2d684641d4c92add54262ed9d5a0d32a66ab4969b9'
	'd75ddf5667f8e4a97eda1e30eb60a23ca8bbaa93f769769cdbbf8c5baea1c963')
[ "$CARCH" = "x86_64" ] && sha256sums[0]='ff37f25a20c626b1f381362d1da05da984d41f83bdacc9fb1fc0cdea09bc0697'

build() {
	# Check and unpack the run package via sh(1)
	sh "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run" --check || return 1
	echo yes | sh "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run" --target "$srcdir" \
		--nox11 --noexec &> /dev/null || return 1

	# Unpack bundled files
	install -d "$pkgdir/opt/VirtualBox" || return 1
	cd "$pkgdir/opt/VirtualBox"
	tar -xjf "$srcdir/VirtualBox.tar.bz2" || return 1

	# Fix compilation issues with kernel versions >= 2.6.36
	patch -Np2 "$pkgdir/opt/VirtualBox/src/vboxnetadp/linux/VBoxNetAdp-linux.c" \
		-i "$srcdir/virtualbox-unlocked_ioctl.patch"

	install -d "$pkgdir/usr/"{bin,share/applications,share/pixmaps} || return 1

	# Hardened build: Mark binaries suid root, create symlinks for working around
	#                 unsupported $ORIGIN/.. in VBoxC.so and make sure the
	#                 directory is only writable by the user (paranoid).
	chmod 4511 VirtualBox VBox{SDL,Headless,NetDHCP}
	for _lib in VBox{VMM,REM,RT,DDU,XPCOM}.so; do
		ln -sf "/opt/VirtualBox/${_lib}" "components/${_lib}"
	done
	chmod go-w .

	# VBoxNetAdpCtl needs to be suid root in any case
	chmod 4511 VBoxNetAdpCtl

	# Replace VirtualBox built-in Qt by system Qt libraries (disabled as of
	# 2010-03-26, 3.1.6-1)
	for _lib in libQt{Core,Gui,Network,OpenGL}; do
		rm "${_lib}VBox.so.4"
		ln -s "/usr/lib/${_lib}.so.4" "${_lib}VBox.so.4"
	done

	# Install the SDK (disabled, since the extension expects an UCS4-compiled
	# python, while Arch Linux has an UCS2-compiled python)
	#cd "$pkgdir/opt/VirtualBox/sdk/installer"
	#VBOX_INSTALL_PATH="/opt/VirtualBox" python vboxapisetup.py install --root "${pkgdir}" || return 1
	#rm -Rf build
	#cd "$pkgdir/opt/VirtualBox"
	rm -Rf sdk vboxshell.py VBoxPython*

	# Install rc.d script for module compilation
	install -Dm0755 "${srcdir}/vboxdrv" "${pkgdir}/etc/rc.d/vboxdrv" || return 1

	# Replace init script stuff
	sed -i -e 's,sudo /etc/init.d/vboxdrv setup,/etc/rc.d/vboxdrv setup,g' \
		"$pkgdir/opt/VirtualBox/VBox.sh"
	sed -i -e 's,sudo /etc/init.d/vboxdrv restart,modprobe vboxdrv,g' \
		"$pkgdir/opt/VirtualBox/VBox.sh"

	# Install udev rules
	install -Dm0644 "$srcdir/10-vboxdrv.rules" "$pkgdir/lib/udev/rules.d/10-vboxdrv.rules" || return 1

	# Symlink the launchers
	for _bin in VirtualBox VBox{Headless,Manage,SDL,SVC,Tunctl,NetAdpCtl} rdesktop-vrdp; do
		ln -s "/opt/VirtualBox/${_bin}" "$pkgdir/usr/bin/${_bin}"
	done

	# Symlink the desktop icon and ".desktop" files
	ln -s "/opt/VirtualBox/VBox.png" "$pkgdir/usr/share/pixmaps/VBox.png"
	ln -s "/opt/VirtualBox/virtualbox.desktop" "$pkgdir/usr/share/applications/VirtualBox.desktop"

	# Symlink the license
	install -d "$pkgdir/usr/share/licenses/$pkgname" || return 1
	ln -s "/opt/VirtualBox/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/PUEL"

	# Setup configuration
	install -d "$pkgdir/etc/vbox" || return 1
	echo 'INSTALL_DIR="/opt/VirtualBox"' > "$pkgdir/etc/vbox/vbox.cfg"
}
