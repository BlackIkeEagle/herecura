# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: xduugu <xduuguΘgmx·com>
# Contributor: Peter 'piie' Feuerer <peterΘpiie·net>
# Contributor: Sascha Pfau <MrPeacockΘgmail·com>
# Contributor: iggy <iggy.mfΘgmail·com>

pkgname=virtualbox_bin
pkgver=4.0.0
_build=69151
pkgrel=1
pkgdesc="Powerful x86 virtualization (Personal Use Binaries Edition)."
arch=('i686' 'x86_64')
url="http://virtualbox.org"
license=('custom:PUEL')
depends=('libidl2' 'libxcursor' 'libxinerama' 'libxslt' 'curl' 'gcc' 'make' 'kernel26-headers' 'qt' 'python2')
optdepends=("sdl: for VBoxSDL and VirtualBox GUI"
	"mesa: for OpenGL support"
	"libxt: for shared Clipboard support"
	"pulseaudio: for PulseAudio support")
provides=("virtualbox=${pkgver}")
conflicts=('virtualbox-ose' 'virtualbox-modules')
install='virtualbox.install'
#source=(
#"VirtualBox-${pkgver}-${_build}-Linux_x86.run::http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}-${_build}-Linux_x86.run"
#"VirtualBox-${pkgver}-${_build}-Linux_amd64.run::http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}-${_build}-Linux_amd64.run"
#)
_arch='x86'
[ "$CARCH" = "x86_64" ] && _arch='amd64'
source=("VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run::http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run"
	'10-vboxdrv.rules'
	'vboxdrv')
sha256sums=('56870c3c6c1c1ba2453e41beba3990430157297e212facde0f0281f0b4086160'
	'f2123c40b37be9144eaa974f90c1708e61101d69a3a31aeade92746e07bf9bb7'
	'9918d9be4802c0c99e6761eda29b3b04a47666cbabda3e13c312c337920c6987')
[ "$CARCH" = "x86_64" ] && sha256sums[0]='0ffba25008dd5651047b7eacd2d77b2931b51a3f6a7da18132303c18c63ba8c8'

build() {
	# Check and unpack the run package via sh(1)
	sh VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run --check
	echo yes | sh VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run --target ${srcdir} \
		--nox11 --noexec &> /dev/null
	# Unpack bundled files
	install -d ${pkgdir}/opt/VirtualBox
	cd ${pkgdir}/opt/VirtualBox
	tar -xjf ${srcdir}/VirtualBox.tar.bz2

	install -d ${pkgdir}/usr/{bin,share/applications,share/pixmaps}

	# Hardened build: Mark binaries suid root, create symlinks for working around
	#                 unsupported $ORIGIN/.. in VBoxC.so and make sure the
	#                 directory is only writable by the user (paranoid).
	chmod 4511 VirtualBox VBox{SDL,Headless,NetDHCP}
	for _lib in VBox{VMM,REM,RT,DDU,XPCOM}.so; do
		ln -sf /opt/VirtualBox/${_lib} components/${_lib}
	done
	chmod go-w .

	# VBoxNetAdpCtl needs to be suid root in any case
	chmod 4511 VBoxNetAdpCtl

	# Replace VirtualBox built-in Qt by system Qt libraries (disabled as of
	# 2010-03-26, 3.1.6-1)
	for _lib in libQt{Core,Gui,Network,OpenGL}; do
		rm ${_lib}VBox.so.4
		ln -s /usr/lib/${_lib}.so.4 ${_lib}VBox.so.4
	done

	# Patch "vboxshell.py" to use Python 2.x (instead of Python 3)
	sed -i 's#/usr/bin/python#\02#' "${pkgdir}/opt/VirtualBox/vboxshell.py"	

	# Install the SDK (disabled, since the extension expects an UCS4-compiled
	# python, while Arch Linux has an UCS2-compiled python)
	cd ${pkgdir}/opt/VirtualBox/sdk/installer
	VBOX_INSTALL_PATH=/opt/VirtualBox python2 vboxapisetup.py install --root ${pkgdir}
	rm -Rf build
	cd ${pkgdir}/opt/VirtualBox
	#rm -Rf sdk vboxshell.py VBoxPython*

	# Install rc.d script for module compilation
	install -Dm0755 ${srcdir}/vboxdrv ${pkgdir}/etc/rc.d/vboxdrv

	# Replace init script stuff
	sed -i -e 's,sudo /etc/init.d/vboxdrv setup,/etc/rc.d/vboxdrv setup,g' \
		${pkgdir}/opt/VirtualBox/VBox.sh
	sed -i -e 's,sudo /etc/init.d/vboxdrv restart,modprobe vboxdrv,g' \
		${pkgdir}/opt/VirtualBox/VBox.sh

	# Install udev rules
	install -Dm0644 ${srcdir}/10-vboxdrv.rules ${pkgdir}/lib/udev/rules.d/10-vboxdrv.rules

	# Symlink the launchers
	for _bin in VirtualBox VBox{Headless,Manage,SDL,SVC,Tunctl,NetAdpCtl} rdesktop-vrdp; do
		ln -s /opt/VirtualBox/${_bin} ${pkgdir}/usr/bin/${_bin}
	done

	# Symlink the desktop icon and .desktop files
	ln -s /opt/VirtualBox/VBox.png ${pkgdir}/usr/share/pixmaps/VBox.png
	ln -s /opt/VirtualBox/virtualbox.desktop ${pkgdir}/usr/share/applications/VirtualBox.desktop

	# Symlink the license
	install -d ${pkgdir}/usr/share/licenses/${pkgname}
	ln -s /opt/VirtualBox/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/PUEL

	# Setup configuration
	install -d ${pkgdir}/etc/vbox
	echo 'INSTALL_DIR="/opt/VirtualBox"' > ${pkgdir}/etc/vbox/vbox.cfg
}
