pkgbase=gstreamer0.10-be-ugly
pkgname=('gstreamer0.10-be-ugly' 'gstreamer0.10-be-ugly-plugins')
pkgver=0.10.17
pkgrel=2
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer0.10-be-base>=0.10.30' 'libdvdread>=4.1.3' 'lame>=3.98.2' 'libmpeg2>=0.5.1' 'a52dec>=0.7.4' 'libid3tag>=0.15.1b' 'libmad>=0.15.1b' 'libsidplay>=1.36.59' 'libcdio>=0.82' 'x264>=20100826' 'opencore-amr')
url="http://gstreamer.freedesktop.org/"
options=(!libtool)
source=(${url}/src/gst-plugins-ugly/gst-plugins-ugly-${pkgver}.tar.bz2)

md5sums=('504ed6508c3a8f035a2002ed69d68506')

build() {
	cd "${srcdir}/gst-plugins-ugly-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--disable-static --enable-experimental \
		--with-package-name="GStreamer Ugly (ArchLinux BE)" \
		--with-package-origin="http://www.archlinux.org/"

	make
	sed -e 's/gst-libs gst ext/gst-libs gst/' -i Makefile
}

package_gstreamer0.10-be-ugly() {
	pkgdesc="GStreamer Multimedia Framework Ugly plugin libraries"
	depends=('gstreamer0.10-be-base>=0.10.28')
	provides=('gstreamer0.10-ugly')
	conflicts=('gstreamer0.10-ugly')

	cd "${srcdir}/gst-plugins-ugly-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-be-ugly-plugins() {
	pkgdesc="GStreamer Multimedia Framework Ugly Plugins (gst-plugins-ugly)"
	depends=("gstreamer0.10-be-ugly=${pkgver}" 'libdvdread>=4.1.3' 'lame>=3.98.2' 'libmpeg2>=0.5.1' 'a52dec>=0.7.4' 'libid3tag>=0.15.1b' 'libmad>=0.15.1b' 'libsidplay>=1.36.59' 'libcdio>=0.82' 'x264>=20100826' 'opencore-amr')
	groups=('gstreamer0.10-be-plugins')
	provides=('gstreamer0.10-ugly-plugins')
	conflicts=('gstreamer0.10-ugly-plugins')

	cd "${srcdir}/gst-plugins-ugly-${pkgver}"
	make -C ext DESTDIR="${pkgdir}" install
}
