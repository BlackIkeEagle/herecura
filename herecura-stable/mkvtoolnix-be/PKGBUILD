# Maintainer: BlackEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=mkvtoolnix-be
pkgver=5.0.0
pkgrel=1
pkgdesc="Set of tools to create, edit and inspect Matroska files, with gui's"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'flac' 'libvorbis' 'file' 'wxgtk' 'boost-libs' 'lzo2' 'xdg-utils')
makedepends=('boost' 'ruby')
conflicts=('mkvtoolnix' 'mkvmerge-gui')
install=mkvtoolnix.install
source=("http://www.bunkus.org/videotools/${pkgname/-be/}/sources/${pkgname/-be/}-${pkgver}.tar.bz2")

build() {
  cd "${srcdir}/${pkgname/-be/}-${pkgver}"

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  #return 1

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib \
			  --enable-gui
  ./drake
}

package () {
  cd "${srcdir}/${pkgname/-be/}-${pkgver}"

  ./drake DESTDIR="${pkgdir}" install
}
sha256sums=('48ba62de06e0c0e318c5484c57e71fa8d1e4ed10aa54442c7f608466a894d969')
