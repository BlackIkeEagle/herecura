pkgname=gstreamer0.10-be
pkgver=0.10.34
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2>=2.7.7' 'glib2>=2.24.1')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
provides=('gstreamer0.10')
conflicts=('gstreamer0.10')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.bz2)
sha256sums=('85348f70dc4b70ad1beb05c9a59a64175c5058f4ee5273f89230a3c1d11b26a3')

build() {
	cd "${srcdir}/gstreamer-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
		--disable-static \
		--with-package-name="GStreamer (ArchLinux BE)" \
		--with-package-origin="http://www.archlinux.org/"
	make
}

package() {
	cd "${srcdir}/gstreamer-${pkgver}"
	make DESTDIR="${pkgdir}" install

	#Remove unversioned gst-* binaries to get rid of conflicts
	cd "${pkgdir}/usr/bin"
	for bins in `ls *-0.10`; do
		rm -f ${bins/-0.10/}
	done
}
