# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=kpackagekit
pkgver=0.6.2
pkgrel=1
pkgdesc="KDE tools for PackageKit, with pacman support by pacman-glib-git. PKGBUILD provided by gog."
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php/KPackageKit?content=84745"
license=('GPL')
depends=('kdelibs' 'packagekit-qt')
makedepends=('cmake' 'automoc4' 'p7zip')
source=(http://kde-apps.org/CONTENT/content-files/84745-${pkgname}-${pkgver}.7z)
sha256sums=('c29bcbf04cabfac8aaf1c03ee0153e9256bf37f9eaaeb6407dcef5a982eb02e8')

build() {
	[ -d "${pkgname}-${pkgver}" ] && rm -rf ${pkgname}-${pkgver}
	7z x 84745-${pkgname}-${pkgver}.7z

	[ -d "build" ] && rm -rf build
	mkdir build
	cd build

	cmake ../${pkgname}-${pkgver} \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR=${pkgdir} install
}
