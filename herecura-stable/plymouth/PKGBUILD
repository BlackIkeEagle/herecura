# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Biginoz <biginoz _at_ free _point_ fr>
pkgname=plymouth
pkgver=0.8.3
pkgrel=6
pkgdesc="A graphical boot splash screen with kernel mode-setting support."
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('initscripts' 'libdrm' 'gtk2')
makedepends=()
conflicts=('bootsplash' 'gensplash' 'fbsplash' 'splashy')
options=('!libtool' '!emptydirs')
install=plymouth.install
source=("http://www.freedesktop.org/software/${pkgname}/releases/${pkgname}-${pkgver}.tar.bz2"
	'plymouth-update-initrd'
	'plymouth-encrypt.initcpio_hook'
	'plymouth-encrypt.initcpio_install'
	'plymouth.functions'
	'plymouth.initcpio_hook'
	'plymouth.initcpio_install'
	'arch-logo.png'
	'system-release')

build() {
	cd ${pkgname}-${pkgver}
	sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' -i src/libply-splash-graphics/ply-image.c
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
		--enable-tracing \
		--enable-tests \
		--with-system-root-install \
		--disable-tests \
		--enable-gdm-transition \
		--without-rhgb-compat-link \
		--with-logo=/usr/share/plymouth/arch-logo.png \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D
	
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=$pkgdir install
	
	cd $pkgdir

	find . -name "*.orig" -delete

	install -D -m755 "$srcdir/plymouth-update-initrd" ${pkgdir}/usr/lib/plymouth
	install -D -m644 $srcdir/plymouth.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth
	install -D -m644 $srcdir/plymouth.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth
	install -D -m644 $srcdir/plymouth-encrypt.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth-encrypt
	install -D -m644 $srcdir/plymouth-encrypt.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth-encrypt
	install -D -m644 $srcdir/plymouth.functions ${pkgdir}/etc/rc.d/functions.d/plymouth.functions
	install -D -m644 "$srcdir/arch-logo.png" ${pkgdir}/usr/share/plymouth/arch-logo.png
	install -D -m644 "$srcdir/system-release" ${pkgdir}/etc/system-release
}
sha256sums=('a9f2fe507c2ee0a2368ff563a42f92bedec553be7dfc9c0b8bb324d2b7fc650c'
            '73e585aa82c5b4f64e9e36cab9156ab6a8d7535446b81926059671ede037affe'
            '12e387eb0a279f9ec9e7600b65fd2dd068a608f8f651de3ff0e14b694fa9e022'
            '8ef0ab3ab0028bd1cb7003fc3ac510b275ae08e4191b1388a77d6b3e1af1bf21'
            '55f6e4ea63706ab62813cddcf88865275e56470f2e041895b39ddceb2566d02b'
            '1a4604d84387e0373c899866c3fd475e0ab2ebd0e58afc7ee69bbd2e631220bc'
            '033f1331174b4a353551d05209664d205e4ef3e723eee3f971b408006a668e92'
            'b8d3b1d0f61c5154b2473f98e4a5b53a912188fb08ccfb6c2e9a9822ccb76e27'
            '4ba2e16e7c6ed9c89e4999bd1ce5dd6e39e2db7447b5e43be384d0fbced67828')
