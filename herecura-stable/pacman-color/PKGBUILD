# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color
pkgver=3.4.2
pkgrel=1
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=("pacman=${pkgver}")
makedepends=('patch')
backup=('etc/pacman.d/color.conf')
source=("ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-${pkgrel}.patch"
	"color.conf")
sha256sums=('27437484be86b61b30db482fcc4436967373e98b67eebe4909ab6c60c35c97b0'
            'f5a35b7b2bfcd624e99b445bbb512d1a4f177cc9740eb851f0c16304b0b1e020'
            'af5e4f53b71582f6eac54a123024bb063adff0cf36a264d3f08eb2817ec43fab')

build() {
	cd pacman-${pkgver}
	patch -p1 -i ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
	make
}

package() {
	cd pacman-${pkgver}

	# install pacman-color
	install -Dm755 ${srcdir}/pacman-${pkgver}/src/pacman/.libs/pacman \
		${pkgdir}/usr/bin/pacman-color

	# install color.conf
	install -Dm644 ${srcdir}/color.conf \
		${pkgdir}/etc/pacman.d/color.conf
}
