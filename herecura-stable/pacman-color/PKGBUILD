# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color
pkgver=3.4.2
pkgrel=1
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman=3.4.1')
makedepends=('patch')
backup=('etc/pacman.d/color.conf')
source=("ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-${pkgrel}.patch"
	"color.conf")
sha256sums=('27437484be86b61b30db482fcc4436967373e98b67eebe4909ab6c60c35c97b0'
            '0bec1b078e9e365e2328a31a1dd148ea882181736ca3a528c058b67fe3be25b6'
            '863574c38bfca0a6485ec4adbfbaa14ca8d573f6f8b9319067acac50d6e2ca26')

build() {
	cd pacman-${pkgver}
	patch -p1 -i ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
	make
}

package() {
	cd pacman-${pkgver}

	# install pacman-color
	install -Dm755 ${srcdir}/pacman-${pkgver}/src/pacman/.libs/pacman \
		${pkgdir}/usr/bin/pacman-color

	# install color.conf
	install -Dm644 ${srcdir}/color.conf \
		${pkgdir}/etc/pacman.d/color.conf
}
