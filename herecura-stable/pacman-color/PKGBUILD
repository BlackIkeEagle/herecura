# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color
pkgver=3.4.3
pkgrel=1
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=("pacman=${pkgver}")
makedepends=('patch')
backup=('etc/pacman.d/color.conf')
source=("ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-${pkgrel}.patch"
	"color.conf")
sha256sums=('9bbe8e1f9859163351bdf3fcb9860491c5220ea35aa22135fa66ec3cc7f1a30b'
            '3e1af97a9a196148ab53b20769c57a3304428470910f4f4e28a4c5cf5022792f'
            'af5e4f53b71582f6eac54a123024bb063adff0cf36a264d3f08eb2817ec43fab')

build() {
	cd pacman-${pkgver}
	patch -p1 -i ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
	make
}

package() {
	cd pacman-${pkgver}

	# install pacman-color
	install -Dm755 ${srcdir}/pacman-${pkgver}/src/pacman/.libs/pacman \
		${pkgdir}/usr/bin/pacman-color

	# install color.conf
	install -Dm644 ${srcdir}/color.conf \
		${pkgdir}/etc/pacman.d/color.conf
}
