# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color
pkgver=3.4.1
pkgrel=2
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman=3.4.1')
makedepends=('patch')
backup=('etc/pacman.d/color.conf')
source=("ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-${pkgrel}.patch"
	"color.conf")
sha256sums=('3818fc751036ec627a81b62007112aca17c16991e8ddb70da76230f9b4978d62'
            'e6f241f7fbd7b907490c775b22ff86c03bce11beeb3c46fd334653f9cd53f36f'
            '863574c38bfca0a6485ec4adbfbaa14ca8d573f6f8b9319067acac50d6e2ca26')

build() {
	cd pacman-${pkgver}
	patch -p1 -i ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
	make
}

package() {
	cd pacman-${pkgver}

	# install pacman-color
	install -Dm755 ${srcdir}/pacman-${pkgver}/src/pacman/.libs/pacman \
		${pkgdir}/usr/bin/pacman-color

	# install color.conf
	install -Dm644 ${srcdir}/color.conf \
		${pkgdir}/etc/pacman.d/color.conf
}
