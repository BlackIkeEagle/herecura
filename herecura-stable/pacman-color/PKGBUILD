# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color
pkgver=3.5.1
pkgrel=1
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=("pacman=${pkgver}")
backup=('etc/pacman.d/color.conf')
source=("ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-${pkgrel}.patch"
	"color.conf")
sha256sums=('e01558d3c394f8f3def589fa97c40c6253466ff670bbce898cfbd592fbcb6a8a'
            'fd1eedb7528c795b69de2dff50a3a3855faef1fe008f79a7ca35cc0e89596e39'
            '66e182733fed303b98f6a0012f91b5239dba87b447ea683a6edb75f63c4774c2')

build() {
	cd pacman-${pkgver}
	patch -p1 -i ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
	make
}

package() {
	# install pacman-color
	install -Dm755 ${srcdir}/pacman-${pkgver}/src/pacman/.libs/pacman \
		${pkgdir}/usr/bin/pacman-color

	# install color.conf
	install -Dm644 ${srcdir}/color.conf \
		${pkgdir}/etc/pacman.d/color.conf
}
