pkgbase=gstreamer0.10-be-good
pkgname=('gstreamer0.10-be-good' 'gstreamer0.10-be-good-plugins')
pkgver=0.10.28
pkgrel=4
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('intltool' 'pkgconfig' 'gstreamer0.10-be-base>=0.10.30' 'libavc1394>=0.5.3-3' 'libiec61883>=1.2.0' 'aalib>=1.4rc5-6' 'libshout>=2.2.2' 'libdv>=1.0.0' 'esound>=0.2.41' 'flac>=1.2.1' 'wavpack>=4.60.1' 'taglib>=1.6.3' 'libsoup>=2.30.1' 'v4l-utils' 'libcaca>=0.99.beta17' 'bzip2>=1.0.5' 'gdk-pixbuf2' 'orc>=0.4.11')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
md5sums=('6ef1588921f59d85c44ee2e49a3c97a0')

build() {
	cd "${srcdir}/gst-plugins-good-${pkgver}"

	#
	# We disable gconf, jack and pulse from the build.
	# We're not using gconf, the jack plugin should be part of gstreamer0.10-bad-plugins,
	# and the pulse plugin is provided by gstreamer0.10-pulse.
	#
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--disable-static --enable-experimental \
		--disable-schemas-install \
		--disable-hal \
		--disable-gconf \
		--disable-jack \
		--disable-pulse \
		--with-package-name="GStreamer Good (ArchLinux BE)" \
		--with-package-origin="http://www.archlinux.org/"

	make
	sed -e 's/gst sys ext/gst/' -i Makefile
}

package_gstreamer0.10-be-good() {
	depends=('gstreamer0.10-be-base>=0.10.29' 'bzip2>=1.0.5' 'orc>=0.4.11')
	pkgdesc="GStreamer Multimedia Framework Good plugin libraries"
	provides=('gstreamer0.10-good')

	cd "${srcdir}/gst-plugins-good-${pkgver}"
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}/etc/gconf"
}

package_gstreamer0.10-be-good-plugins() {
	depends=("gstreamer0.10-be-good=${pkgver}" 'libavc1394>=0.5.3-3' 'libiec61883>=1.2.0' 'aalib>=1.4rc5-6' 'libshout>=2.2.2' 'libdv>=1.0.0' 'esound>=0.2.41' 'flac>=1.2.1' 'wavpack>=4.60.1' 'taglib>=1.6.3' 'libsoup>=2.30.1' 'v4l-utils' 'libcaca>=0.99.beta17' 'libpng>=1.4.1' 'libjpeg>=8.0.1' 'orc>=0.4.11')
	pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good)"
	groups=('gstreamer0.10-be-plugins')
	provides=('gstreamer0.10-good-plugins')

	cd "${srcdir}/gst-plugins-good-${pkgver}"
	make -C sys DESTDIR="${pkgdir}" install
	make -C ext GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
	install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
	install -m644 gconf/gstreamer-0.10.schemas "${pkgdir}/usr/share/gconf/schemas/gstreamer0.10-good-plugins.schemas"
}
