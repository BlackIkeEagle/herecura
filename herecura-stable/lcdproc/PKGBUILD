# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: BlackEagle <ike DOT devolder AT herecura DOT be>
# AUR Category: system
pkgname=lcdproc
pkgver=0.5.3
pkgrel=4
pkgdesc="LCDproc is a utility to drive one or more LCD (and LCD-like) devices attached to a host."
url="http://lcdproc.omnipotent.net/"
license="GPL"
depends=('libusb' 'libftdi' 'lirc')
makedepends=('autoconf' 'pkgconfig' 'automake')
backup=(etc/LCDd.conf)
arch=('i686' 'x86_64')
source=(
	"http://switch.dl.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	'lcdd'
	'imonlcd.c.patch'
)
md5sums=(
	'fe9a7c9d8f2c5e76250ce2ea8f644921'
	'56f52dcccd6e129a9cf84cd1a0452d56'
	'7e7979a570788d19d2cb579a54eb4dea'
)

build() {
	cd ${pkgname}-${pkgver}
	autoreconf
	patch -Np0 -i ${srcdir}/imonlcd.c.patch || return 1
	./configure --prefix=/usr --sysconfdir=/etc --enable-libusb --enable-lcdproc-menus --enable-stat-smbfs --enable-drivers=all
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	sed -e "s/server\/drivers\//\/usr\/lib\/lcdproc\//g" LCDd.conf > ${pkgdir}/etc/LCDd.conf
	mkdir -p ${pkgdir}/etc/rc.d
	install -m 755 ${startdir}/lcdd ${pkgdir}/etc/rc.d
}
