pkgbase=gstreamer0.10-be-bad
pkgname=('gstreamer0.10-be-bad' 'gstreamer0.10-be-bad-plugins')
pkgver=0.10.22
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL' 'GPL')
makedepends=('pkgconfig' 'gstreamer0.10-be-base>=0.10.34' 'xvidcore' 'libdca' 'bzip2' 'libdc1394' 'neon' 'faac' 'musicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 'libdvdnav' 'libmodplug' 'jasper' 'liblrdf' 'libofa' 'soundtouch' 'libvdpau' 'schroedinger' 'libass' 'libvpx' 'gsm' 'libgme' 'rtmpdump' 'libsndfile' 'librsvg' 'raptor1')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-bad/gst-plugins-bad-${pkgver}.tar.bz2)
md5sums=('9a2acee1f386f71247003d0d7090fb1c')

build() {
	cd "${srcdir}/gst-plugins-bad-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--disable-static --enable-experimental \
		--with-package-name="GStreamer Bad (ArchLinux BE)" \
		--with-package-origin="http://www.archlinux.org/"

	make
	sed -e 's/gst sys ext/gst/' -i Makefile
}

package_gstreamer0.10-be-bad() {
	pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad)"
	depends=('gstreamer0.10-be-base>=0.10.34')
	provides=('gstreamer0.10-bad')
	conflicts=('gstreamer0.10-bad')

	cd "${srcdir}/gst-plugins-bad-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-be-bad-plugins() {
	pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad)"
	depends=("gstreamer0.10-be-bad=${pkgver}" 'xvidcore>=1.2.2' 'libdca>=0.0.5' 'bzip2' 'libdc1394>=2.1.2' 'exempi>=2.1.1' 'jack>=0.118.0' 'neon>=0.29.3' 'faac>=1.28' 'libexif>=0.6.19' 'musicbrainz>=2.1.5' 'faad2>=2.7' 'libmms>=0.5' 'libcdaudio>=0.99.12' 'libmpcdec>=1.2.6' 'mjpegtools>=1.9.0' 'libdvdnav>=4.1.3' 'libmodplug>=0.8.8' 'jasper>=1.900.1' 'liblrdf>=0.4.0' 'libofa>=0.9.3' 'libvdpau>=0.4' 'soundtouch>=1.5.0' 'libass>=0.9.9' 'schroedinger>=1.0.9' 'libvpx' 'gsm' 'rtmpdump' 'libgme' 'libsndfile' 'librsvg')
	groups=('gstreamer0.10-be-plugins')
	provides=('gstreamer0.10-bad-plugins')
	conflicts=('gstreamer0.10-bad-plugins')
	install=gstreamer0.10-bad-plugins.install

	cd "${srcdir}/gst-plugins-bad-${pkgver}"
	make -C gst-libs DESTDIR="${pkgdir}" install
	make -C ext DESTDIR="${pkgdir}" install
	make -C sys DESTDIR="${pkgdir}" install
	make -C gst-libs DESTDIR="${pkgdir}" uninstall
}
