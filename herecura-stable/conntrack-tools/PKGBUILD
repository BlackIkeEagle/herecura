# Maintainer: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=conntrack-tools
pkgver=0.9.15
pkgrel=1
pkgdesc="Userspace tools to interact with the Netfilter's Connection Tracking System"
arch=('i686' 'x86_64')
url="http://www.netfilter.org/projects/conntrack-tools"
license=('GPL')
depends=(libnetfilter_conntrack)
source=(http://www.netfilter.org/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.bz2)
md5sums=('caecb479ceb5ff7f646d47e85484d655')

build() {
  # let's go
  cd ${srcdir}/${pkgname}-${pkgver} || return 1

  # build package
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install directories
  install -d ${pkgdir}/etc/conntrackd || return 1
  install -d ${pkgdir}/usr/share/${pkgname}/doc || return 1

  # install doc, default config and scripts
  cp -R ${srcdir}/${pkgname}-${pkgver}/doc/* ${pkgdir}/usr/share/${pkgname}/doc/ || return 1

  # remove useless doc files
  rm ${pkgdir}/usr/share/${pkgname}/doc/manual/config.xsl \
     ${pkgdir}/usr/share/${pkgname}/doc/manual/conntrack-tools.tmpl \
     ${pkgdir}/usr/share/${pkgname}/doc/manual/docbook.css \
     ${pkgdir}/usr/share/${pkgname}/doc/manual/Makefile || return 1

  # set scripts executable
  chmod a+x ${pkgdir}/usr/share/${pkgname}/doc/debian.conntrackd.init.d \
            ${pkgdir}/usr/share/${pkgname}/doc/cli/test.sh \
            ${pkgdir}/usr/share/${pkgname}/doc/sync/multiprimary.sh \
            ${pkgdir}/usr/share/${pkgname}/doc/sync/primary-backup.sh || return 1

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}
