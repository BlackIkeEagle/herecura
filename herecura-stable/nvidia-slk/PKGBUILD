# Maintainer : BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-slk
pkgver=260.19.12
_kernver='2.6.36-SLK'
pkgrel=2
pkgdesc="NVIDIA drivers for kernel26-slk"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-slk>=2.6.36' 'kernel26-slk<2.6.37' "nvidia-utils=${pkgver}")
makedepends=('kernel26-slk-headers>=2.6.36' 'kernel26-slk-headers<2.6.37')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install

#source=(
#"http://uk.download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run"
#"http://uk.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}.run"
#)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("http://uk.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('5ce99ae80acb4a9e0d5334247a800baa4c24a97c274292fed12a6e264ed0aa5d')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("http://uk.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('909dd5f45e33445871027cbef37c28990d73fe462b1dc9202f17aeecdba2ac7d')
fi

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
 	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
		$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
}
