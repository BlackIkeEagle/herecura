# Maintainer: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: (asper, noob

pkgname=vuurmuur
_pkgname=Vuurmuur
pkgver=0.7
pkgrel=7
pkgdesc="A powerful firewall manager built on top of iptables"
arch=('i686' 'x86_64')
url="http://www.vuurmuur.org"
license=('GPL')
depends=('iptables')
optdepends=('conntrack-tools: for connection tracking'
            'iproute2: for traffic shapping'
            'iptrafvol: for traffic volume logging')
backup=('etc/vuurmuur/config.conf'
        'etc/vuurmuur/modules.conf'
        'etc/vuurmuur/vuurmuur_conf.conf'
        'etc/vuurmuur/plugins/textdir.conf'
        'etc/logrotate.d/vuurmuur')
source=(http://downloads.sourceforge.net/${pkgname}/${_pkgname}-${pkgver}.tar.gz \
        config.conf \
        modules.conf \
        rc.${pkgname})
install=vuurmuur.install
md5sums=('bad91aafcbea5e3a434440f88d722778'
         '3b873578a13bcfc27b22d085f42560a2'
         '20675e323030d71e740706e8096033d3'
         '8a9526896b8043bfc08f22b2b4c9b7e2')

build() {
  # extract tarballs
  cd ${srcdir}/${_pkgname}-${pkgver} || return 1
  for TARBALL in lib${pkgname}-${pkgver}.tar.gz ${pkgname}-${pkgver}.tar.gz ${pkgname}_conf-${pkgver}.tar.gz ; do
    tar xzf $TARBALL || return 1
  done

  # build vuurmuur library
  cd ${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver} || return 1
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # build vuurmuur
  cd ${srcdir}/${_pkgname}-${pkgver}/${pkgname}-${pkgver} || return 1
  sed -i -e 's|-rpath|-rpath-link|' configure || return 1
  ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-libvuurmuur-includes="${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src" \
        --with-libvuurmuur-libraries="${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src" \
        --with-widec=yes \
        LDFLAGS="-L${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src \
                 -L${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src/.libs \
                 -Wl,-rpath=/usr/lib/${pkgname}/plugins" || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # build vuurmuur_conf
  cd ${srcdir}/${_pkgname}-${pkgver}/${pkgname}_conf-${pkgver} || return 1
  sed -i -e 's|-rpath|-rpath-link|' configure || return 1
  ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-libvuurmuur-includes="${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src" \
        --with-libvuurmuur-libraries="${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src" \
        --with-widec=yes \
        LDFLAGS="-L${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src \
                 -L${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/src/.libs \
                 -Wl,-rpath=/usr/lib/${pkgname}/plugins" || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install configuration
  install -d -m700 ${pkgdir}/etc/${pkgname} || return 1
  install -D -m600 ${srcdir}/config.conf  ${pkgdir}/etc/${pkgname}/config.conf || return 1
  install -D -m600 ${srcdir}/modules.conf ${pkgdir}/etc/${pkgname}/modules.conf || return 1
  install -D -m600 ${srcdir}/${_pkgname}-${pkgver}/${pkgname}_conf-${pkgver}/config/${pkgname}_conf.conf.sample \
                   ${pkgdir}/etc/${pkgname}/${pkgname}_conf.conf || return 1

  sed -i -e 's|/usr/bin/iptrafvol.pl|/usr/sbin/iptrafvol.pl|' \
             ${pkgdir}/etc/${pkgname}/${pkgname}_conf.conf || return 1

  install -d -m700 ${pkgdir}/etc/${pkgname}/plugins || return 1
  echo "LOCATION=\"/etc/${pkgname}/textdir\"" > ${pkgdir}/etc/${pkgname}/plugins/textdir.conf || return 1

  install -D -m644 ${pkgdir}/usr/share/${pkgname}/scripts/${pkgname}-logrotate ${pkgdir}/etc/logrotate.d/${pkgname} || return 1

  # default firewall setup in /usr/share
  install -d ${pkgdir}/usr/share/${pkgname}/textdir/{interfaces,services,zones,rules} || return 1

  touch ${pkgdir}/usr/share/${pkgname}/textdir/rules/rules.conf || return 1
  touch ${pkgdir}/usr/share/${pkgname}/textdir/rules/blocklist.conf || return 1

  cp -R ${srcdir}/${_pkgname}-${pkgver}/zones/*   ${pkgdir}/usr/share/${pkgname}/textdir/zones/ || return 1
  cp -R ${pkgdir}/usr/share/${pkgname}/services/* ${pkgdir}/usr/share/${pkgname}/textdir/services/ || return 1

  # install daemon, log and doc
  install -D -m755 ${srcdir}/rc.${pkgname} ${pkgdir}/etc/rc.d/${pkgname} || return 1
  install -d -m700 ${pkgdir}/var/log/${pkgname} || return 1

  for FILE in ${pkgdir}/usr/share/${pkgname}/help/*.hlp
  do
    mv ${FILE} ${FILE}.iso8859 || return 1
    iconv -f ISO-8859-1 -t UTF-8 ${FILE}.iso8859 > ${FILE} || return 1
  done

  # install licenses
  install -D -m644 ${srcdir}/${_pkgname}-${pkgver}/lib${pkgname}-${pkgver}/COPYING \
                   ${pkgdir}/usr/share/licenses/lib${pkgname}/COPYING || return 1

  install -D -m644 ${srcdir}/${_pkgname}-${pkgver}/${pkgname}-${pkgver}/COPYING \
                   ${pkgdir}/usr/share/licenses/${pkgname}/COPYING || return 1

  install -D -m644 ${srcdir}/${_pkgname}-${pkgver}/${pkgname}_conf-${pkgver}/COPYING \
                   ${pkgdir}/usr/share/licenses/${pkgname}_conf/COPYING || return 1

  # remove useless stuffs
  rm -rf ${pkgdir}/usr/share/doc || return 1
  rm -rf ${pkgdir}/usr/share/${pkgname}/services || return 1
  rm -f  ${pkgdir}/usr/share/${pkgname}/help/vuurmuur-ru.UTF-8.hlp || return 1
}
