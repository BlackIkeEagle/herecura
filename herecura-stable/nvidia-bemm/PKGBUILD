# Maintainer : BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-bemm
pkgver=260.19.36
_kernver='2.6.35-BEMM'
pkgrel=2
pkgdesc="NVIDIA drivers for kernel26-bemm"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-bemm>=2.6.35' 'kernel26-bemm<2.6.36' "nvidia-utils=${pkgver}")
makedepends=('kernel26-bemm-headers>=2.6.35' 'kernel26-bemm-headers<2.6.36')
conflicts=('nvidia-96xx' 'nvidia-173xx')
replaces=('nvidia-slk')
license=('custom')
install=nvidia.install

#source=(
#"http://uk.download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run"
#"http://uk.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}.run"
#)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("http://uk.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('2826484d87827351774449382e45c4a9')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("http://uk.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('6ebc5db9066e920b0b0e48377ce5eeb5')
fi

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
 	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
		$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf

	# gzip all modules
	find ${pkgdir} -name '*.ko' -exec gzip -9 {} \;

	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
}
