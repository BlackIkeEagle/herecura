pkgname=gstreamer0.10-be-pulse
pkgver=0.10.28
pkgrel=1
pkgdesc="A GStreamer 0.10 plugin for the PulseAudio sound server"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig')
url="http://gstreamer.freedesktop.org/"
depends=('gstreamer0.10-be-base>=0.10.30' 'libpulse>=0.9.21')
optdepends=('pulseaudio: pulseaudio support')
options=(!libtool !emptydirs)
groups=('pulseaudio')
provides=('gstreamer0.10-pulse')
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
md5sums=('6ef1588921f59d85c44ee2e49a3c97a0')

build() {
	cd "${srcdir}/gst-plugins-good-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--disable-static --enable-experimental \
		--with-package-name="GStreamer Pulse (ArchLinux BE)" \
		--with-package-origin="http://www.archlinux.org/"

	cd ext/pulse && make
}

package() {
	cd "${srcdir}/gst-plugins-good-${pkgver}/ext/pulse"
	make DESTDIR="${pkgdir}" install
}
