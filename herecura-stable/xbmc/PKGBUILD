# Contributors : Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgname=xbmc
pkgver=10.1
pkgrel=5
pkgdesc="XBMC Media Center Dharma branch"
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse')
replaces=('xbmc-dharma')
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'LGPL')
depends=(libgl libmpeg2 fribidi lzo2 smbclient sdl_image libva libxtst sdl_mixer libmicrohttpd libcdio libmms libmysqlclient libsamplerate glew wavpack libssh mesa-demos xorg-xdpyinfo libmad libmodplug faac faad2 jasper sqlite3 rtmpdump fontconfig enca)
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'yasm' 'zip' 'unzip' 'libxrandr' 'libxinerama' 'vdpau-video' 'gcc45')
optdepends=(
	'lirc: remote controller support' 
	'vdpau-video: for nvidia hardware accelleration'
	'unrar: access compressed files without unpacking them'
	'udisks: automount external drives'
	'upower: used to trigger suspend functionality'
	'lcms'
)
install=("${pkgname}.install")
source=( "http://mirrors.xbmc.org/releases/source/${pkgname}-${pkgver}.tar.gz" "FEH.sh" )
sha256sums=('3403195b7eb1d076523f2394793b7f050df00743892b031f6fea86b998559d8a'
            'a7e5f2114fe19640fc24300b7c69686f37d63df901b2f95bfbbc6cf9ae1c3ede')
options=(strip)

_prefix='/usr'

build() {
	# Configure XBMC
	#
	# Note on external-libs:
	#   - We cannot use external python because Arch's python was built with
	#     UCS2 unicode support, whereas xbmc expects UCS4 support
	#   - According to an xbmc dev using external/system ffmpeg with xbmc is "pure stupid" :D

	cd ${pkgname}-${pkgver}

	# remove "--as-needed" from LDFLAGS, since it causes
	# undefined symbols at runtime:
	# /usr/lib/xbmc/system/players/dvdplayer/avcodec-52-i486-linux.so:
	#  undefined symbol: NeAACDecOpen
	unset LDFLAGS; LDFLAGS="-Wl,--hash-style=gnu"

	# fix linking issue with gcc 4.5, see git 6ea61fce78q
	sed -i 's: --soname,: -Wl,--soname,:' \
	xbmc/cores/dvdplayer/Codecs/{,libdvd/}Makefile.in

	# fix lsb_release dependency
	sed -i -e 's:/usr/bin/lsb_release -d:cat /etc/arch-release:' xbmc/utils/SystemInfo.cpp

	export CC=gcc-4.5
	export CXX=g++-4.5

	msg2 "Bootstrapping XBMC"
	./bootstrap

	msg2 "Configuring XBMC" 
	./configure --prefix=${_prefix} --exec-prefix=${_prefix} \
		--disable-debug \
		--enable-optimizations \
		--enable-gl \
		--enable-vdpau \
		--enable-vaapi \
		--disable-profiling \
		--enable-joystick \
		--enable-rtmp \
		--disable-xrandr \
		--disable-pulse \
		--disable-hal \
		--disable-avahi

	# Now (finally) build
	msg2 "Running make" 
	make
	msg2 "building python modules" 
	make -C lib/addons/script.module.pil
	make -C lib/addons/script.module.pysqlite
}

package() {
	cd ${pkgname}-${pkgver}
	msg2 "Running make install" 
	make DESTDIR=${pkgdir} install

	# Replace FEH.py with FEH.sh (and thus remove external python dependency)
	msg2 "Replace FEH.py with FEH.sh (and thus remove external python dependency)"
	install -Dm755 ${srcdir}/FEH.sh \
		${pkgdir}${_prefix}/share/xbmc/FEH.sh
	sed -i -e 's/^python \(.*\)FEH.py \(.*\)$/\1FEH.sh \2/' \
		${pkgdir}${_prefix}/bin/xbmc

	# lsb_release fix
	sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
	sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" "${pkgdir}${_prefix}/bin/xbmc"

	# Tools
	msg2 "Tools"
	install -Dm755 ${srcdir}/${pkgname}-${pkgver}/tools/TexturePacker/TexturePacker \
		${pkgdir}${_prefix}/lib/xbmc/

	# Licenses
	msg2 "Copy licenses"
	install -dm755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
	for licensef in LICENSE.GPL copying.txt; do
		mv ${pkgdir}${_prefix}/share/doc/xbmc/${licensef} \
			${pkgdir}${_prefix}/share/licenses/${pkgname}
	done

	#cleanup some stuff
	msg2 "cleanup unneeded files"
	rm -rf ${pkgdir}/usr/share/xsessions
	rm -f ${pkgdir}/usr/share/xbmc/FEH.py
	# its not very usefull, and broken at the moment
	rm -f ${pkgdir}/usr/bin/xbmc-standalone
	# didnt we built --disable-xrandr ?
	rm -f ${pkgdir}/usr/lib/xbmc/xbmc-xrandr
}
