# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Jorge Barroso <jorge.barroso.11 at gmail dot com>
# Contributor: onestep_ua <onestep@ukr.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: lh <jarryson#gmail.com>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>
# Based on packagekit PKGBUILD by Jonathan Conder <jonno.conder@gmail.com>


_gitname=packagekit
pkgbase=$_gitname-git
pkgname=(
  "$_gitname-cron-git"
  "$_gitname-gstreamer-plugin-git"
  "$_gitname-gtk2-git"
  "$_gitname-gtk3-git"
  "$_gitname-python-git"
  "$_gitname-git"
)
pkgver=20141003.fab44ee
pkgrel=1
arch=('i686' 'x86_64')
license=('GPLv2')
url="http://www.packagekit.org/"
makedepends=("glib2" "dbus-glib" "pam" "sqlite" "networkmanager" "polkit" "systemd" "pacman>=4.1.0"
"gstreamer" "gst-plugins-base" "gtk3" "gtk2" "python"
"xmlto" "gtk-doc" "intltool" "bash-completion" 'git'
'gobject-introspection'
)
options=('!libtool' '!emptydirs')
source=($_gitname::git+https://github.com/hughsie/PackageKit.git)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

prepare() {
  cd "$srcdir/$_gitname"

  mkdir -p "$srcdir/out"
}

build() {
  cd "$srcdir/$_gitname"
  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/PackageKit \
    --disable-static \
    --disable-schemas-compile \
    --disable-browser-plugin \
    --enable-gtk-doc \
    --enable-python3 \
    --enable-systemd \
    --enable-systemd-updates \
    --enable-gstreamer-plugin \
    --enable-alpm
  make

  make DESTDIR="${srcdir}/out" install
}

package_packagekit-cron-git() {
  pkgdesc="Cron job and related utilities for PackageKit"
  depends=('cron' 'packagekit-git')

  install -dm755 "$pkgdir/etc"

  cd "$srcdir/out"
  mv etc/cron.daily "$pkgdir/etc"
  mv etc/sysconfig "$pkgdir/etc"
}

package_packagekit-gstreamer-plugin-git() {
  pkgdesc="Install GStreamer codecs using PackageKit"
  depends=('gst-plugins-base' 'packagekit-git')

  install -dm755 "$pkgdir/usr/lib/PackageKit"

  cd "$srcdir/out"
  mv usr/lib/PackageKit/pk-gstreamer-install \
    "$pkgdir/usr/lib/PackageKit"

  cd "$pkgdir/usr/lib/PackageKit"
  ln -s pk-gstreamer-install gst-install-plugins-helper
}

package_packagekit-gtk2-git() {
  pkgdesc="Gtk2 bindings for PackageKit"
  depends=('gtk2' 'packagekit-git')

  install -dm755 "$pkgdir/usr/lib"

  cd "$srcdir/out"
  mv usr/lib/gtk-2.0 "$pkgdir/usr/lib"
}

package_packagekit-gtk3-git() {
  pkgdesc="Gtk3/Gnome3 bindings for PackageKit"
  depends=('gtk3' 'packagekit-git')

  install -dm755 "$pkgdir/usr/lib"

  cd "$srcdir/out"
  mv usr/lib/gtk-3.0 "$pkgdir/usr/lib"
  mv usr/lib/gnome-settings-daemon-3.0 "$pkgdir/usr/lib"
}

package_packagekit-python-git() {
  pkgdesc="python bindings for PackageKit"
  depends=('python' 'packagekit-git')

  install -dm755 "$pkgdir/usr/lib"

  cd "$srcdir/out"
  mv usr/lib/python* "$pkgdir/usr/lib"
}

package_packagekit-git() {
  pkgdesc="Package management service"
  depends=("glib2" "dbus-glib" "pam" "sqlite" "networkmanager" "polkit" "systemd" "pacman>=4.1.0")

  conflicts=("packagekit")
  replaces=("packagekit")
  provides=("packagekit")

  cd "$srcdir/out"
  mv ./* "$pkgdir"
}

# vim:set ts=2 sw=2 et:
