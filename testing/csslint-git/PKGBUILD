# vim:set ft=sh:
# Maintainer: N30N <archlinux@alunamation.com>
# Contributor: rickeyski <rickeyvisinski@gmail.com>

pkgname=csslint-git
_gitname="csslint"
pkgver=20130815.f1ef64d
pkgrel=1
pkgdesc="Automated linting of Cascading Stylesheets."
url="http://csslint.net/"
license=("MIT")
depends=("nodejs")
makedepends=("git" "apache-ant")
arch=("any")
provides=("csslint")
source=("$_gitname::git://github.com/stubbornella/csslint.git");
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	echo $(git log -1 --format="%ci" | sed 's/.*\([0-9]\{4\}\)-\([0-9]\{2\}\)-\([0-9]\{2\}\).*/\1\2\3/').$(git describe --always )
}

build() {
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	/usr/share/git/workdir/git-new-workdir ${_gitname} ${_gitname}-build master
	cd "${_gitname}-build"
	ant build.node
}

package() {
	cd "${_gitname}-build"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	cd build/npm/
	sed -i "s/@VERSION@/0.9.3/" package.json
	npm install -g --prefix "${pkgdir}/usr"
}
