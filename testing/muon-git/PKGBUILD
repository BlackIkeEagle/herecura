# Maintainer: Antonio Rojas 

_gitname=muon
pkgname=$_gitname-git
pkgver=20141112.eaaf421
pkgrel=1
pkgdesc='A collection of package management tools for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/sysadmin/muon'
license=('LGPL')
depends=('packagekit-qt-git' 'kdeclarative')
makedepends=('extra-cmake-modules' 'git' 'python')
conflicts=('muon')
provides=('muon')
source=("$_gitname::git://anongit.kde.org/muon.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_gitname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
