# $Id$
# Maintainer: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sébastien Luttringer <seblu@aur.archlinux.org>

pkgbase=virtualbox-modules-bede-mainline
pkgname=('virtualbox-modules-bede-mainline-host' 'virtualbox-modules-bede-mainline-guest')
pkgver=4.2.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-bede-mainline' 'linux-bede-mainline-headers'
    "virtualbox-host-source>=$pkgver"
    "virtualbox-guest-source>=$pkgver")
source=('modules-load-virtualbox-bede-mainline'
    '60-vboxguest.rules')
md5sums=('f2200ed91b6ec089d16cc3ada5418c73'
    'ed1341881437455d9735875ddf455fbe')

_extramodules=3.7-BEDE-MAINLINE-external

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    # dkms need modification to be run as user
    cp -r /var/lib/dkms .
    echo "dkms_tree='$srcdir/dkms'" > dkms.conf
    # build host modules
    msg2 'Host modules'
    dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
    # build guest modules
    msg2 'Guest modules'
    dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-modules-bede-mainline-host() {
    pkgdesc="Kernel host modules for VirtualBox (linux-bede-mainline)"
    license=('GPL')
    install=virtualbox-modules-bede-mainline-host.install
    depends=('linux-bede-mainline')
    provides=("virtualbox-host-modules=$pkgver")

    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
    install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

    # install config file in modules-load.d for out of the box experience
    install -Dm644 "$srcdir/modules-load-virtualbox-bede-mainline" \
        "$pkgdir/usr/lib/modules-load.d/virtualbox-modules-bede-mainline-host.conf"

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/virtualbox-modules-bede-mainline-host.install"
}

package_virtualbox-modules-bede-mainline-guest() {
    pkgdesc="Kernel guest modules for VirtualBox (linux-bede-mainline)"
    license=('GPL')
    install=virtualbox-modules-bede-mainline-guest.install
    depends=('linux-bede-mainline')
    provides=("virtualbox-guest-modules=${pkgver}")

    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
    install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/vbox"
    find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

    install -D -m 0644 "$srcdir/60-vboxguest.rules" \
        "$pkgdir/usr/lib/udev/rules.d/60-vboxguest-bede-mainline.rules"

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/virtualbox-modules-bede-mainline-guest.install"
}

# vim:set ft=sh et:
