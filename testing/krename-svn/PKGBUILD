# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >

pkgname=krename-svn
_svnmod=krename
pkgver=245
pkgrel=1
pkgdesc="KRename is a powerful batch file renamer for KDE4."
url="http://www.krename.net/"
arch=('i686' 'x86_64') 
license=('GPL')
depends=('kdelibs' 'taglib')
makedepends=('subversion' 'cmake' 'automoc4')
options=('libtool')
conflicts=('krename')
provides=('krename')
source=("$_svnmod::svn://svn.code.sf.net/p/krename/code/trunk")
md5sums=('SKIP')

pkgver() {
	cd "$_svnmod"
	svnversion | tr -d [A-z]
}

build() {
	cd "$_svnmod"

	export CPLUS_INCLUDE_PATH="$CPLUS_INCLUDE_PATH:/usr/include/freetype2/"
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd "$_svnmod"
	make DESTDIR="$pkgdir" install
}
