# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper-git
pkgver=20130222
pkgrel=1
pkgdesc="A tool for managing btrfs snapshots. It can compare snapshots and revert differences between snapshots."
provides=('snapper')
conflicts=('snapper')
arch=('i686' 'x86_64')
url="http://en.opensuse.org/Portal:Snapper"
license=('GPL2')
depends=('libxml2' 'openssl' 'pcre' 'python')
makedepends=('git' 'boost' 'swig')
backup=('etc/conf.d/snapper')
source=()
install=snapper.install

if [ -e .githash_${CARCH} ] ; then
	_gitphash=$(cat .githash_${CARCH})
else
	_gitphash=""
fi

_gitroot='https://github.com/openSUSE/snapper.git'
_gitname='snapper'

build() {
	if [ -d ${srcdir}/${_gitname}/.git ] ; then
		( cd ${srcdir}/${_gitname} && git pull origin )
		msg "The local files are updated."
	else
		( git clone --depth 1 ${_gitroot} ${_gitname} )
	fi
	msg "GIT checkout done or server timeout"

	cd ${_gitname}
	if [ "${_gitphash}" == $(git show | grep -m 1 commit | sed 's/commit //') ]; then
		msg "Git hash is the same as previous build"
		return 1
	fi

	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	/usr/share/git/workdir/git-new-workdir ${_gitname} ${_gitname}-build master

	msg "Starting make..."
	cd ${_gitname}-build
	# use /etc/conf.d instead of /etc/sysconfig
	sed -i -e 's@/etc/sysconfig@/etc/conf.d@g' \
		  snapper/SnapperDefines.h \
		  scripts/* \
		  doc/*
	# don't install zypper plugin
	sed -i '/usr\/lib\/zypp/d' scripts/Makefile.am
	# rename cron scripts
	sed -i -e 's@suse.de-@@g' scripts/Makefile.am

  # boost fixlets
  # don't link against non-existant libbost_thread-mt
  sed -i -e 's@lboost_thread-mt@lboost_thread@g' snapper/Makefile.am
  # add -lboost_system
  sed -E -i 's@snapperd_LDADD(.*)@snapperd_LDADD\1 \-lboost_system@g' server/Makefile.am
  sed -E -i 's@snapper_LDADD(.*)@snapper_LDADD\1 \-lboost_system@g' client/Makefile.am

	sed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' -i configure.in
	aclocal
	libtoolize --force --automake --copy
	autoheader
	automake --add-missing --copy
	autoconf
	export CPPFLAGS="-I/usr/include/python3.2mu"
	./configure --prefix=/usr
	make
}

package() {
	cd ${_gitname}-build
	msg2 "Running make install"
	make DESTDIR=${pkgdir} install
	mkdir ${pkgdir}/etc/conf.d
	install -D -m 644 data/sysconfig.snapper ${pkgdir}/etc/conf.d/snapper
	git show | grep -m 1 commit | sed 's/commit //' > ${startdir}/.githash_${CARCH}
}

# vim:set ts=2 sw=2 et:
