# vim:set ft=sh:
# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper-git
_gitname='snapper'
pkgver=20131127.8033124
pkgrel=1
pkgdesc="A tool for managing BTRFS and LVM snapshots. It can create, diff and restore snapshots and provides timelined auto-snapping."
provides=('snapper')
conflicts=('snapper')
arch=('i686' 'x86_64')
url="http://snapper.io"
license=('GPL2')
depends=('btrfs-progs' 'libxml2' 'dbus')
makedepends=('boost' 'lvm2' 'libxslt' 'docbook-xsl' 'pam' 'git')
backup=('etc/conf.d/snapper')
source=("$_gitname::git://github.com/openSUSE/snapper.git"
        'btrfs-master.patch')
sha256sums=('SKIP'
            'e8b441f4f8f615ea2d2bdd819105d085ec890a0f0e601dc54ea46c6abf543891')
install=snapper.install

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

build() {
	cd "$_gitname"

	# rename cron scripts
	sed -i -e 's@suse.de-@@g' scripts/Makefile.am
	# fix config location in cron scripts
	sed -i -e 's@/etc/sysconfig/@/etc/conf.d/@g' scripts/snapper-{daily,hourly}

	# boost fixlets - Arch doesn't use -mt suffix
	sed -i -e 's@lboost_thread-mt@lboost_thread@g' snapper/Makefile.am
	sed -i -e 's@lboost_system-mt@lboost_system@g' snapper/Makefile.am

	# fix pam plugin install location
	sed -i -e 's@shell echo /@shell echo /usr/@g' pam/Makefile.am
	# Arch all in /usr/bin
	sed -i -e 's@/usr/sbin@/usr/bin@g' data/org.opensuse.Snapper.service

	aclocal
	libtoolize --force --automake --copy
	autoheader
	automake --add-missing --copy
	autoconf
	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--with-conf=/etc/conf.d \
		--disable-zypp \
		--disable-silent-rules \
		--disable-ext4 \
		--enable-xattrs
	make
}

package() {
	cd "$_gitname"
	make DESTDIR="$pkgdir" install
	install -Dm 644 data/sysconfig.snapper "$pkgdir/etc/conf.d/snapper"
}
