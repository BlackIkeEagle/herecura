# vim:set ft=sh:
#Maintainer: David Lehmann <dtl@voxelbrain.com>
#Contributor: Sebastian Friedel <sef@voxelbrain.com>
#Contributor: Mathias Stearn <mathias@10gen.com>
pkgname=mongodb-git
_gitname="mongo"
pkgver=20131211.172bf6a
pkgrel=1
pkgdesc="A high-performance, open source, schema-free document-oriented database."
arch=('i686' 'x86_64')
url="http://www.mongodb.org"
license=('AGPL3')
makedepends=('git' 'scons' 'boost')
depends=('boost-libs' 'pcre' 'snappy' 'openssl')
provides=('mongodb')
conflicts=('mongodb')
install=${pkgname}.install
backup=('etc/mongodb.conf')
source=(
	"$_gitname::git://github.com/mongodb/mongo.git"
	'mongodb.service'
	'mongodb.conf'
)
sha256sums=(
	'SKIP'
	'19f55ab28652b3817e98fc3f15cc2f6f3255a5e1dfd7b0d5a27c9ba22fd2703e'
	'8010ce728d657524cd76b5afda7ffbc1cc389642336b12b89cec5df2b09fc0e4'
)

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

build() {
	cd ${_gitname}

	#sed -i 's|lib64|lib|g' SConstruct
	#sed -i '/-Werror/d' SConstruct

	export SCONSFLAGS="${MAKEFLAGS}"
	scons all --full
}

package() {
	cd ${_gitname}
	scons install --full --prefix="$pkgdir/usr"
	install -Dm644 "$srcdir/mongodb.service" "$pkgdir/usr/lib/systemd/system/mongodb.service"
	install -Dm644 "$srcdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
	install -dm700 "$pkgdir/var/lib/mongodb"
	install -dm755 "$pkgdir/var/log/mongodb"
}
