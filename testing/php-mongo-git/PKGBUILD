# vim:set ft=sh:
# Maintainer: Dino Krtanjek <krtanjekdino@gmail.com>
# Maintainer: Richard Rowe <arch.richard@gmail.com>
pkgname=php-mongo-git
_gitname="mongo-php-driver"
pkgver=20130416.6c09d03
pkgrel=1
pkgdesc="Officially supported PHP driver for MongoDB"
arch=("i686" "x86_64")
url="http://www.mongodb.org/display/DOCS/PHP+Language+Center"
license=("APACHE")
depends=("php")
makedepends=( 'git' )
backup=("etc/php/conf.d/mongo.ini")
source=(
	"$_gitname::git://github.com/mongodb/mongo-php-driver.git"
	"mongo.ini"
)
md5sums=(
	'SKIP'
	'6a34ebbedfbeb7b2462fbba936afea04'
)

pkgver() {
	cd "$srcdir/$_gitname"
	echo $(git log -1 --format="%ci" | sed 's/.*\([0-9]\{4\}\)-\([0-9]\{2\}\)-\([0-9]\{2\}\).*/\1\2\3/').$(git rev-parse --short HEAD)
}

build() {
	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	/usr/share/git/workdir/git-new-workdir ${_gitname} ${_gitname}-build master

	msg "Starting make..."
	cd ${_gitname}-build
	phpize
	./configure --prefix=/usr --enable-mongo
}

package() {
	cd ${_gitname}-build
	make INSTALL_ROOT=$pkgdir install
	install -Dm644 $srcdir/mongo.ini "$pkgdir/etc/php/conf.d/mongo.ini"
}
