# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >

_gitname=apper
pkgname=$_gitname-git
pkgver=20141120.a88d08d
pkgrel=1
pkgdesc="Apper - Application to get and manage software in KDE"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/sysadmin/apper"
license=('GPL')
depends=('kdelibs' 'packagekit-qt4-git')
makedepends=('git' 'cmake' 'automoc4' 'kdebase-workspace')
provides=('kpackagekit')
conflicts=('kpackagekit')
replaces=('kpackagekit')
source=($_gitname::git://anongit.kde.org/apper)                                                    
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

prepare() {
	cd "$_gitname"

	mkdir qt4
}

build() {
	cd "$_gitname/qt4"

	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$_gitname/qt4"

	make DESTDIR=${pkgdir} install
}
