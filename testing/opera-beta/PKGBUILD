# Maintainer: ruario 
# Contributor: RobertMe
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
# Contributor: eworm

pkgname=opera-beta
pkgver=25.0.1614.54
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. beta version."
url="http://blogs.opera.com/desktop/"
depends=('gtk2' 'desktop-file-utils' 'shared-mime-info' 'libxtst' 'gconf' 'libxss' 'gcc-libs' 'alsa-lib' 'nss' 'freetype2' 'ttf-font')
optdepends=(
	'curl: opera crash reporter and autoupdate checker'
	'ffmpeg: playback of not really open formats'
)
provides=('opera')
install=${pkgname}.install
options=(!strip !zipman)
license=('custom:opera')
backup=("etc/$pkgname/default")
arch=('x86_64')
source=(
	"http://get.geo.opera.com/pub/$pkgname/$pkgver/linux/${pkgname}_${pkgver}_amd64.deb"
	"opera"
	"default"
)
sha256sums=(
	'c6975e7ec6ee92dc26110bc42a17c3231ba42977596b844b082498b387f6c920'
	'508512464e24126fddfb2c41a1e2e86624bdb0c0748084b6a922573b6cf6b9c5'
	'4913d97dec0ddc99d1e089b029b9123c2c86b7c88d631c4d1111b119b09da027'
)

prepare() {
	sed -e "s/%pkgname%/$pkgname/g" -i "$srcdir/opera"
	sed -e "s/%operabin%/x86_64-linux-gnu\/$pkgname\/$pkgname/g" \
		-i "$srcdir/opera"
}

package() {
	tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"

	# fix opera to use libudev.so.1
	sed -e 's/libudev.so.0/libudev.so.1/g' \
		-i "$pkgdir/usr/lib/x86_64-linux-gnu/opera-beta/opera-beta"

	# add compat symlinks for ffpmeg
	ln -sf "/usr/lib/libavcodec.so" "$pkgdir/usr/lib/libavcodec-ffmpeg.so.55"
	ln -sf "/usr/lib/libavformat.so" "$pkgdir/usr/lib/libavformat-ffmpeg.so.55"
	ln -sf "/usr/lib/libavutil.so" "$pkgdir/usr/lib/libavutil-ffmpeg.so.52"

	# suid opera_sandbox
	chmod 4755 "$pkgdir/usr/lib/x86_64-linux-gnu/opera-beta/opera_sandbox"

	# install default options
	install -Dm644 "$srcdir/default" "$pkgdir/etc/$pkgname/default"

	# install opera wrapper
	rm "$pkgdir/usr/bin/$pkgname"
	install -Dm755 "$srcdir/opera" "$pkgdir/usr/bin/$pkgname"

	# license
	install -Dm644 \
		"$pkgdir/usr/lib/x86_64-linux-gnu/opera-beta/opera_autoupdate.licenses" \
		"$pkgdir/usr/share/licenses/opera-beta/opera_autoupdate.licenses"
}
