# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >

pkgname=krusader-git
pkgver=20120421
pkgrel=1
pkgdesc="Krusader - Twin Panel File Management For Your Desktop - GIT version"
arch=('i686' 'x86_64')
url="http://www.krusader.org/"
license=('GPL')
depends=('kdelibs')
makedepends=('git' 'cmake' 'automoc4')
provides=('krusader')
conflicts=('krusader')
replaces=('krusader' 'krusader-svn')
source=()
md5sums=()

if [ -e .githash_${CARCH} ] ; then
	_gitphash=$(cat .githash_${CARCH})
else
	_gitphash=""
fi

_gitname="krusader"
_gitroot="git://anongit.kde.org/krusader"

build() {
	if [ -d ${srcdir}/${_gitname}/.git ] ; then
		( cd ${srcdir}/${_gitname} && git pull origin )
		msg "The local files are updated."
	else
		( git clone --depth 1 ${_gitroot} ${_gitname} )
	fi
	msg "GIT checkout done or server timeout"

	cd ${_gitname}
	if [ "${_gitphash}" == $(git show | grep -m 1 commit | sed 's/commit //') ]; then
		msg "Git hash is the same as previous build"
		return 1
	fi

	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	cp -a ${_gitname} ${_gitname}-build

	msg "Starting make..."
	cd ${_gitname}-build
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd ${_gitname}-build
	make DESTDIR=${pkgdir} install
	git show | grep -m 1 commit | sed 's/commit //' > ${startdir}/.githash_${CARCH}
}
