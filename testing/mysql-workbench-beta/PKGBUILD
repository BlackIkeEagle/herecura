# vim:set ft=sh:
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname='mysql-workbench-beta'
_pkgname='mysql-workbench'
pkgver=6.0.5
pkgrel=1
pkgdesc="A cross-platform, visual database design tool developed by MySQL"
arch=('i686' 'x86_64')
url="https://www.mysql.com/products/workbench/"
license=('GPL2')
depends=('ctemplate' 'desktop-file-utils' 'gtkmm' 'hicolor-icon-theme'
	'libgnome-keyring' 'libiodbc' 'libzip' 'lua51' 'mysql-connector-c++'
	'python2' 'tinyxml' 'unixodbc' 'vsqlite++'
	'python2-paramiko' 'python2-pexpect' 'python2-pysqlite')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager')
makedepends=('boost' 'curl' 'mesa' 'cmake')
install='mysql-workbench.install'
options=('!libtool !distcc !ccache')
provides=('mysql-workbench')
conflicts=('mysql-workbench')

source=("http://cdn.mysql.com/Downloads/MySQLGUITools/${_pkgname}-community-${pkgver}-src.tar.gz")
sha256sums=('1f65dc93937c49e9df3b46ce9fc8897f1545e2eb6459d857c7cbfcaa0fbe26ad')

build() {
	cd "$_pkgname-community-$pkgver-src"

	cmake . \
		-DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
		-DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
		-DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
		-DLUA_LIBRARY=/usr/lib/liblua5.1.so \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$_pkgname-community-$pkgver-src"

	make DESTDIR="${pkgdir}" install

	cd .. && rm -rf "$_pkgname-community-$pkgver-src"
}
