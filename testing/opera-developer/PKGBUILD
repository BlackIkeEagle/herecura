# Maintainer: ruario 
# Contributor: RobertMe
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
# Contributor: eworm

pkgname=opera-developer
pkgver=31.0.1876.0
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. developer version."
url="http://blogs.opera.com/desktop/"
provides=('opera')
replaces=('opera-devel' 'opera-dev')
install=${pkgname}.install
options=(!strip !zipman)
license=('custom:opera')
backup=("etc/$pkgname/default")
arch=('i686' 'x86_64')
depends=('gtk2' 'desktop-file-utils' 'shared-mime-info' 'libxtst' 'gconf' 'libxss' 'gcc-libs' 'alsa-lib' 'nss' 'freetype2' 'ttf-font')
optdepends=(
	'curl: opera crash reporter and autoupdate checker'
	'ffmpeg: playback of proprietary formats'
)

source=(
	"opera"
	"default"
)
source_i686=("http://deb.opera.com/opera/pool/non-free/o/${pkgname}/${pkgname}_${pkgver}_i386.deb")
source_x86_64=("http://deb.opera.com/opera/pool/non-free/o/${pkgname}/${pkgname}_${pkgver}_amd64.deb")

sha256sums=('508512464e24126fddfb2c41a1e2e86624bdb0c0748084b6a922573b6cf6b9c5'
            '4913d97dec0ddc99d1e089b029b9123c2c86b7c88d631c4d1111b119b09da027')
sha256sums_i686=('6a8cebc47e2cfb121617f0c51f1974d74ef742037757fcca3a011c0f7341705a')
sha256sums_x86_64=('9cfad413d4f23f2881bf53bea94227e6dd6259c1461b80bdc216b8816ea6be46')

_arch=$CARCH
[[ "$CARCH" == "i686" ]] && _arch='i386'

prepare() {
	sed -e "s/%pkgname%/$pkgname/g" -i "$srcdir/opera"
	sed -e "s/%operabin%/$_arch-linux-gnu\/$pkgname\/$pkgname/g" \
		-i "$srcdir/opera"

}

package() {
	tar -xf data.tar.xz --exclude=usr/share/{lintian,menu} -C "$pkgdir/"

	# suid opera_sandbox
	chmod 4755 "$pkgdir/usr/lib/$_arch-linux-gnu/$pkgname/opera_sandbox"

	# install default options
	install -Dm644 "$srcdir/default" "$pkgdir/etc/$pkgname/default"

	# install opera wrapper
	rm "$pkgdir/usr/bin/$pkgname"
	install -Dm755 "$srcdir/opera" "$pkgdir/usr/bin/$pkgname"

	# license
	install -Dm644 \
		"$pkgdir/usr/share/doc/$pkgname/copyright" \
		"$pkgdir/usr/share/licenses/$pkgname/copyright"
}

