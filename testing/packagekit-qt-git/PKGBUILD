# Maintainer : BlackIkeEagle <ike DOT devolder AT gmail DOT com>

_gitname=packagekit-qt
pkgbase=$_gitname-git
pkgname=(
	"${_gitname}4-git"
	"${_gitname}-git"
)
pkgver=20140920.63a9350
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPLv2')
url="http://www.packagekit.org/"
makedepends=(
"packagekit-git" 'git' 'qt4' 'cmake' 'qt5-base'
)
source=($_gitname::git+https://github.com/hughsie/PackageKit-Qt.git)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

prepare() {
	cd "$srcdir"

	mkdir -p "$_gitname/qt4"
	mkdir -p "$_gitname/qt5"
}

build() {
	cd "$srcdir/$_gitname/qt4"
	cmake -DCMAKE_INSTALL_LIBDIR:PATH=lib ..
	make

	cd "$srcdir/$_gitname/qt5"
	cmake -DCMAKE_INSTALL_LIBDIR:PATH=lib -DUSE_QT5=1 ..
	make
}

package_packagekit-qt4-git() {
	pkgdesc="QT4 bindings for PackageKit"
	depends=('packagekit-git' 'qt4')

	cd "$srcdir/$_gitname/qt4"
	make DESTDIR="$pkgdir" install
}

package_packagekit-qt-git() {
	pkgdesc="QT5 bindings for PackageKit"
	depends=('packagekit-git' 'qt5-base')

	cd "$srcdir/$_gitname/qt5"
	make DESTDIR="$pkgdir" install
}
